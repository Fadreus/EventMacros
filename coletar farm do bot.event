automacro nomeDoMercador {
	priority -20 #prioridade altíssima
	run-once 1
	BaseLevel > 0
	exclusive 1
	CheckOnAI auto,manual,off
	call {
		##COLOQUE DENTRO DOS PARENTESIS O NOME DO  SEU MERCADOR
		##SE NÃO COLOCAR A MACRO NAO VAI FUNCIONAR
		$nomeDoMercador = &nick(NipoSorcerer)


		##ESSAS SERÃO AS COORDENADAS PADRÃO, VOCE TEM QUE MUDAR SENÃO DÁ TRETA
		##EXEMPLO : prontera 100 100
		$coordenadaPertoDoMercador = CIDADE E COORDENADAS


		##ESSA É A MENSAGEM QUE VAI ATIVAR TODA A MACRO, É O QUE VC RECEBE
		## DO MERCADOR
		$mensagem = vem aqui


		## COLOQUE: $pesoDoItem{IDdoItem} = PESODOITEM
		## Exemplo: se eu quiser fazer com sangue de lobo:
		##
		## $pesoDoItem{6252} = 1 #Sangue de Lobo
		##
		## ONDE:
        ## 6252 É A ID DO ITEM SANGUE DE LOBO, 
        ## E 1 É O PESO DELE
		## SE QUISER ADICIONAR MAIS ITENS, É SÓ SEGUIR O MESMO PADRÃO
		## E SÓ!!!
		$pesoDoItem{604} = 5 #Galho Seco
		$pesoDoItem{578} = 2 #Morango
		$pesoDoItem{931} = 1 #Amuleto dos Orcs
		$pesoDoItem{1032} = 1 #Flor de Planta Carnívora
		$pesoDoItem{7097} = 1 #Coração Incandescente

        ## ESSAS SÃO AS CONTAS QUE VC QUER QUE ELE PASSE
        ## COLOQUE USUARIO E SENHA DE CADA UMA DELAS
        ## PODE SER QUANTAS VOCÊ QUISER
        ## EXEMPLO:
        ##
        ## &push(@usename, &nick(nipodemos))
        ## &push(@senha, 123456)
        ##
        ## &push(@login, &nick(xXxmalokeroxXx))
        ## &push(@senha, malokero5454)
        ##
        ##SÓ SEGUIR ESSE PADRÃO QUE FICA SUAVE
        &push(@login , &nick(usuario1))
        &push(@senha, senha1)

        &push(@login, &nick(usuario2))
        &push(@senha, senha2)




########################################################################################
########################################################################################
########################################################################################
########################################################################################
##################                                                  ####################
###############  A PARTIR DAQUI PRA BAIXO NAO PRECISA MEXER EM NADA   ##################
##################                                                  ####################
########################################################################################
########################################################################################
########################################################################################
########################################################################################

		[
		log =======================
		log = config: &config(coordenadaPertoDoMercador)
		log =======================
		]
		
		if ( &config(coordenadaPertoDoMercador) =~ /\w+ \d+ \d+/ ) {
		    $coordenadaPertoDoMercador = &config(coordenadaPertoDoMercador)
		}
		
		#esse switch é pra decidir qual kafra será usada pra pegar os itens
		#será de preferencia da mesma cidade que vai encontrar com o mercador
		#o local padrão é prontera
		switch ($coordenadaPertoDoMercador) {
		    case (=~ /prontera/) {
				$posicaoDaKafra = prontera 151 29
		    }
		    case (=~ /payon/) {
		        $posicaoDaKafra = payon 181 104
		    }
		    case (=~ /geffen/) {
				$posicaoDaKafra = geffen 120 62
		    }
			case (=~ /izlude/) {
				$posicaoDaKafra = izlude 134 88
			}
			case (=~ /alberta/) {
				$posicaoDaKafra = alberta 113 60
			}
			else {
				## cidade padrão pra dar storage é prontera
				$posicaoDaKafra = prontera 151 29
			}
		}

	}
}



automacro PegandoFarm {
	PrivMsg /^\s*$mensagem\s*$/ #Código enviado por PM para ativar a Macro
	exclusive 1
	call {
		## assim que receber a mensagem do seu mercador
		## ele vai começar todo o processo
		if ($.PrivMsgLastName = $nomeDoMercador) {
			do conf -f passarItens sim
			do pm $.PrivMsgNameLastName ok
			do pm $.PrivMsgNameLastName ok, ponto de encontro é: $coordenadaPertoDoMercador
			do pm $.PrivMsgNameLastName só vou vender primeiro
			do conf -f coletarFarm_passo autosell
			
		} elsif ($nomeDoMercador =~ /NOME DO BOT QUE RECEBE OS ITENS/){
		    #trigga se o usuário tiver esqueecido de fazer a configuração
			[
			log ================================
			log =VOCÊ ESQUECEU DE CONFIGURAR O NOME DO MERCADOR
			log =INTERROMPENDO MACRO IMEDIATAMENTE
			log ================================
			]
            do eval Misc::offlineMode();
            stop
            
		} else {
			[
			log ================================
			log =UMA PESSOA ALEATORIA MANDOU A MENSAGEM PRA MIM
			log =IGNORANDO COMPLETAMENTE
			log ================================
			]
			
		}
	}
}

automacro PegandoFarm_CoordenadaPersonalizada {
	PrivMsg /\w+ \d+ \d+/  #/ pra detectar as novas coordenadas
	CheckOnAI auto, manual
	priority -5
	exclusive 1
	call {
		## O mercador enviou uma coordenada personalizada, então será pra essa corrdenada que vc vai
		## O ideal seria se a coordenada fosse 1 celula ao lado, em vez da posição exata do mercador
		## mas whatever
		$coordenadaPertoDoMercador = checarMensagem("$.PrivMsgLastMsg")
		
		if ($coordenadaPertoDoMercador = -1) stop
        do conf -f coordenadaPertoDoMercador $coordenadaPertoDoMercador
        
        ##esse if existe pra checar se a pessoa que te mandou pm é o seeu mercador
        ##minha macro é a prova de inúteis qu tentam bugar a parada
		if ($.PrivMsgLastName = $nomeDoMercador) {
		    
			do conf -f passarItens sim
			do pm $nomeDoMercador ok, ponto de encontro é: $coordenadaPertoDoMercador
			do pm $nomeDoMercador só vou vender primeiro
			do conf -f coletarFarm_passo autosell
			
		} elsif ($nomeDoMercador =~ /NOME DO BOT QUE RECEBE OS ITENS/){
		    #mais um check pra ver se o usuario nao fez cagada
			[
			log ================================
			log =VOCÊ ESQUECEU DE CONFIGURAR O NOME DO MERCADOR
			log =INTERROMPENDO MACRO IMEDIATAMENTE
			log ================================
			]
            do eval Misc::offlineMode();
			stop
			
		} else {
			[
			log ==========================================
			log =UMA PESSOA ALEATORIA MANDOU A MENSAGEM PRA MIM
			log =IGNORANDO COMPLETAMENTE
			log ==========================================
			]
			
		}
	}
}

automacro fazerAutoSell {
	ConfigKey passarItens sim
	ConfigKey coletarFarm_passo autosell
	exclusive 1
	priority 20 #prioridade baixa
	run-once 1
	CheckOnAI auto,manual
	call {
		do ai on
		do autosell
	}
}

#AI_storage_done

automacro completeiAsVendas {
	ConfigKey passarItens sim
	ConfigKey coletarFarm_passo autosell
	SimpleHookEvent AI_sell_auto_complete
	exclusive 1
	priority -5 #prioridade alta
	call {
		##completado as vendas, vamos ver se precisa de storage
		do conf -f coletarFarm_passo irNoStorage
	    release fazerAutoSell
	}
}

automacro IndoProArmazen {
	ConfigKey passarItens sim
	ConfigKey coletarFarm_passo irNoStorage
	CheckOnAI auto,manual
	exclusive 1
    call {
		do ai manual
		#checar se o armazem ja foi aberto nessa sessão
		$armazemJaFoiAberto = checarSeArmazenJaFoiAberto()
        log Armazen foi aberto? R: $armazemJaFoiAberto

		if ($armazemJaFoiAberto = sim ) {
			## ja que o armazen foi aberto, vamos checar se a gente precisa ir nele
			## as vezes nao tem nenhum item no armzem que a gente queira
			## dificil mas possível, e economiza tempo
			@itens = &keys(%pesoDoItem)
			
			$i = 0
			while ($i < @itens) {
				if ( &storamount($itens[$i]) > 0) {
					$precisaIrNoStorage = sim
					$i = @itens
				}
				$i++
			}
			$i = undef
			
		} else {
			##se nao foi aberto nessa sessão, precisamos abrir de qualquer jeito
			##então vamos la
			$precisaIrNoStorage = sim
		}
		
		if ($precisaIrNoStorage = sim) {
			do pm $nomeDoMercador ja vendi, agora vou pegar do armazem
			do move $posicaoDaKafra &rand(1,5)
			
		} else {
			do pm $nomeDoMercador ja vendi, to indo ai
			if ($coordenadaPertoDoMercador = CIDADE E COORDENADAS) {
				[
				log ===============================================
				log =VOCÊ ESQUECEU DE DEFINIR AS COORDENADAS PADRÃO
				log =SEM ELAS O BOT NÃO SABE ONDE ELE VAI IR
				log =INTERROMPENDO MACRO IMEDIATAMENTE
				log ===============================================
				]
				do eval Misc::offlineMode();
				stop
				
			} else {
				do conf -f coletarFarm_passo irNoMercador
				release MercadorPerto
				do move $coordenadaPertoDoMercador #Mapa e Coordenadas para troca com o receptor
				
			}
		}
		release MercadorPerto
	}
}

automacro FalarComKafra_abrirStorage {
    NpcNear /Kafra/
    ConfigKey coletarFarm_passo irNoStorage
    ConfigKey passarItens sim
    exclusive 1
    call {
        do pm $nomeDoMercador ja abri o storage, falta pouco
        do talk &npc(/Kafra/)
        do talk resp 1
        #storage aberto, agora outra macro vai cuidar disso
    }
}

automacro PegandoItensDoStorage {
    StorageOpened 1
	exclusive 1
	ConfigKey passarItens sim
	CheckOnAI auto,manual
    call {
		@itens = &keys(%pesoDoItem)
		$i = 0
		while ($i < @itens && $.weightpercent < 90) {
			if ( &storamount($itens[$i]) > 0) {
				#vai pegar o item da lista de itens definida anteriormente (se tiver no armazem)
				#e vai pegar a quantidade máxima que o bot suporta de peso
				#essa parte do peso que é mais complicada, olha esse eval doidão kkkkk
				do storage get getNameByID("$itens[i]") &eval(int(($.maxweight-$.weight)/$pesoDoItem{$itens[$i]}))

				while (&storamount($itens[$i]) > 0 && $.weightpercent < 90) {
					## se entrar nesse if, provavelmente é um equipamento que só pode ser pego 1 por vez
					## então nao vou colocar a parte calcular o peso, ja quee é irrelevante
					do storage get getNameByID("itens[i]")
				}
			}
			$i++
		}
		$i = undef
		do storage close
    }
}

automacro pegueiItensDoStorage {
	SimpleHookEvent AI_storage_done
	exclusive 1
	ConfigKey passarItens sim
	CheckOnAI auto,manual
	call {
		do pm $nomeDoMercador ja peguei tudo que dava, to indo ai
		do conf -f coletarFarm_passo irNoMercador
	}
}

automacro irNoMercador {
    ConfigKey passarItens sim
    ConfigKey coletarFarm_passo irNoMercador
    PlayerNotNear /$nomeDoMercador/
    exclusive 1
    CheckOnAI auto,manual
    call {
        do move $coordenadaPertoDoMercador
    }
}

automacro MercadorPerto {
    PlayerNear /$nomeDoMercador/
	timeout 40
	ConfigKey passarItens sim
	ConfigKey coletarFarm_passo irNoMercador
	CheckOnAI auto,manual
    call {
        do move $.PlayerNearLastPos 1
		do deal &player($nomeDoMercador)
    }
}

automacro DandoItem {
	SimpleHookEvent engaged_deal
	ConfigKey passarItens sim
	CheckOnAI auto,manual
	PlayerNear /$nomeDoMercador/
	exclusive 1
	call {
		if ($.PlayerNearLastDist < 3) {
            do conf -f coletarFarm_passo dentroDaNegociacao
			$zenyQueVouDar = &eval($.zeny - 100000)
			if ($zenyQueVouDar > 0 ) do deal add z $zenyQueVouDar
			
			@itens = &keys(%pesoDoItem)
            
			$i = 0
			$itensNaNegociacao = 0
			while ($i < @itens && $itensNaNegociacao < 10) {
			    ## debug: log dentro do while pra passar itens
				$idDoItemAtual = $itens[$i]
				$nomeDoItemAtual = getNameByID("$idDoItemAtual")
				$qtdDoItemAtualNoInventario = &invamount($nomeDoItemAtual)
				
				## debug: log item: $nomeDoItemAtual, quantidade: $qtdDoItemAtualNoInventario
				
				if ( $qtdDoItemAtualNoInventario > 0) {
				    ## debug: log dentro do if pra passar itens
					do deal add &inventory($nomeDoItemAtual)
					$itensNaNegociacao++
				}
				$i++
			}
			$i = 0
			do pm $nomeDoMercador já passei tudo, finaliza aí
			do deal
		} else {
			[
			log =======================================
			log =Tamo no meio da negociação, mas o mercador ta longe
			log =provavelmente é alguem tentando levar a melhor
			log =IGNORANDO COMPLETAMENTE
			log =======================================
			]
			stop
		}
	}
}

automacro Negociacao_clicarNoBotaoFinalizar {
    SimpleHookEvent finalized_deal
    exclusive 1
    CheckOnAI auto,manual
    ConfigKey passarItens sim
    ConfigKey coletarFarm_passo irNoMercador
    call {
        do conf -f coletarFarm_passo passeiOsItens
        do deal
    }
}

automacro NegociaçãoFinalizada_checandoPorMaisItens {
    SimpleHookEvent complete_deal
    CheckOnAI auto,manual
    ConfigKey passarItens sim
	ConfigKey coletarFarm_passo passeiOsItens
	exclusive 1
    call {
		release MercadorPerto
		do conf -f passarItens none
		do conf -f coletarFarm_passo none
		
        # esse if checa se tem mais itens pra passar que nao cabe numa
        # negociacao só (quando vc quer passar mais de 10 itens)
		if ($itensNaNegociacao >= 10 && @itens > 10 ) {
            log tem mais itens pra passar
            do pm $nomeDoMercador tem mais itens pra te passar mano
            do conf -f coletarFarm_passo irNoMercador
            stop
        }

        # esse while checa se tem itens no storage que nao 
        # nao deu pra levar por conta que tava pesado
        # se tiver, ele faz a checagem do storage denovo
        $i = 0
        while ($i < @itens) {
            $idDoItemAtual = $itens[$i]
            $nomeDoItemAtual = getNameByID("$idDoItemAtual")
            if (&storamount($nomeDoItemAtual) > 0) {
                [
                log ==========================================
                log =ainda tem mais itens pra pegar do armazem
                log ==========================================
                ]
                $i = @itens
                do conf -f coletarFarm_passo irNoStorage
                stop
            }
        }
        
        ## se chegar nessa parte, significa que ja entregamos todos os itens
        ## tanto do storage quanto do do inventario
        ## então precisamos trocar de conta!!
        [
        log ================================
        log =Pronto, tudo entregue, movendo pra próxima conta
        log ================================
        ]
        do conf -f coletarFarm_passo trocarDeConta
    }
}

automacro trocarDeConta {
    Configkey coletarFarm_passo trocarDeConta
    exclusive 1
    CheckOnAI auto,manual
    call {
        #esse if checa se o usuário lembrou de colocar 
        #uma senha pra todos os username
        #checa se a quantidade de usernames é igual a de senhas
        if ( $login[0] = usuario1) {
            [
            log ====================================================
            log VOCE TEM QUE COLOCAR USERNAME E SENHA PRA TODAS AS CONTAS
            log INTERROMPENDO MACRO IMEDIATAMENTE
            log ====================================================
            ]
            do eval Misc::offlineMode();
            stop
        }
        if ( @login = @senha) {
            #se for igual vamos checar em wual conta estou e pular pra próxima
            $i = 0
            while ($i < @login) {
                if (&config(username) = $login[$i]) {
                    $i++
                    do conf username $login[$i]
                    do conf password $senha[$i]
                    $i = @login #gambiarra pra terminar o while
                }
                $i++
            }
        do pm $nomeDoBot vou trocar para o usuario: &config(username)
        do charselect
        } else {
            [
            log ====================================================
            log VOCE TEM QUE COLOCAR USERNAME E SENHA PRA TODAS AS CONTAS
            log MESMO QUE A SENHA SEJA IGUAL
            log PROVAVELMENTE VC ESQUECEEU DE COLOCAR ALGUMA SENHA
            log INTERROMPENDO MACRO IMEDIATAMENTE
            log ====================================================
            ]
            do eval Misc::offlineMode();
            stop
        }
    }
}

automacro ErroAoTentarNegociar {
    SimpleHookEvent error_deal
	CheckOnAI manual
	PlayerNotNear /$nomeDoMercador/
	exclusive 1
    call {
		[
        log ===============================================
		log tentamos negociar mas o mercador não está por perto
		log movendo-se pra o local especificado denovo: ($coordenadaPertoDoMercador)
		log ===============================================
		do move $coordenadaPertoDoMercador
		]
    }
}

automacro ErroAoTentarNegociar2 {
    SimpleHookEvent error_deal
	CheckOnAI manual
	PlayerNearDist /$nomeDoMercador/ > 2
	exclusive 1
    call {
        [
        log ================================
        log = tentei negociar com o mercador, mas ele ta longe demais
		log = tentando mover-se para mais perto ($.PlayerNearDistLastPos)
        log ================================
        ]
		do move $.PlayerNearDistLastPos
    }
}

sub checarSeArmazenJaFoiAberto {
	if ($char->storage->wasOpenedThisSession()) {
		return "sim";
	} else {
		return "nao";
	}
}

sub checarMensagem {
	my ($mensagem) = @_;
	message "mensagem: $mensagem\n";
	if ($mensagem =~ /(\w+ \d+ \d+)/) {
		return $1;
	} else {
		error "Erro ao tentar definir as coordenadas personalizadas.\n";
		error "provavelmente não foi enviado corretamente CIDADE E COORDENADA\n";
		return -1;
	}
}

sub getNameByID {
	my ($id) = @_;
	if ($id =~ /\d+/) {
		return $items_lut{$id};
	} else {
		warning "Erro ao tentar conseguir o nome do item ('$id')\n".
		"verifique se a ID foi digitada corretamente\n";
		return -1;
	}
}
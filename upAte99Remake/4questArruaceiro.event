#From- Macro Quest Arruaceiro 2.0

#By Gaspel
#Edited- by CALF
#convertido pra eventMacro por Nipodemos
automacro virarArruaceiroInicio_Bugged {
	FreeSkillPoints > 0
	timeout 30
	JobLevel = 50
	JobID 6
	exclusive 1
	call {
		[
		log ===========================================
		log =Estou no lvl de virar arruaceiro, porem
		log =os pontos não estão distribuídos...
		log =tentando resolver isso
		log ===========================================
		]
		do conf skillsAddAuto 1
		do conf skillsAddAuto_list NV_BASIC 9, TF_DOUBLE 10, TF_MISS 10, TF_STEAL 10, TF_HIDING 8, TF_POISON 10, TF_DETOXIFY 1, RG_SNATCHER 10, RG_STEALCOIN 10, RG_BACKSTAP 4, RG_TUNNELDRIVE 5, RG_RAID 5, RG_INTIMIDATE 5, RG_PLAGIARISM 10
		[
		log ===========================================
		log =se isso estiver aparecendo repetitamente, a macro tem um bug
		log ===========================================
		]
	}
}
automacro virarArruaceiroInicio_IrNoNpc {
	JobID 6 #gatuno
	JobLevel = 50
	FreeSkillPoints = 0
	priority 2
	exclusive 1
	NpcNotNear /Arruaceira da Guilda/
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	call {
		call pararDeAtacar
		do conf lockMap none
		do conf -f virarArruaceiro true
		do move in_rogue &rand(361,366) &rand(112,117)
	}
}

automacro virarArruaceiroInicio {
	JobID 6 #gatuno
	JobLevel = 50
	priority 2
	exclusive 1
	NpcNear /Arruaceira da Guilda/
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	call {
		#do talknpc 363 122 c r0 r0 r0 r2 r3 r1 r3 r2 r1 r0 r1 c c c #Arruaceira da Guilda
		do talk $.NpcNearLastBinId
		do talk resp 0
		do talk resp 0
		do talk resp 0
		do talk resp 2
		do talk resp 3
		do talk resp 1
		do talk resp 3
		do talk resp 2
		do talk resp 1
		do talk resp 0
		do talk resp 1
	}
}

automacro virarArruaceiro2 {
	JobID 6 #gatuno
	QuestActive 2017
	exclusive 1
	run-once 1
	call {
		do move in_rogue &rand(370,373) &rand(20,24)
		do talk &npc ( 376 23 ) #Smith
	}
}


automacro virarArruaceiro3 {
	JobID 6 #gatuno
	QuestActive 2018
	exclusive 1
	ConfigKeyNot questArruaceiro peguei
	timeout 100
	call  {
		do iconf 510 7 1 0 #ervaAzul
		do iconf 957 10 1 0 #unhaApodrecida
		do iconf 932 10 1 0 #osso
		do iconf 958 10 1 0 #mandibula
		$ervaAzul       = &invamount (Erva Azul)
		$unhaApodrecida = &invamount (Unha Apodrecida)
		$osso           = &invamount (Osso)
		$mandibula      = &invamount (Mandíbula Horrenda)
		if ( $ervaAzul < 6 ) goto Erva
		if ( $ervaAzul >= 6 && $unhaApodrecida < 10 ) goto Unha
		if ( $ervaAzul >= 6 && $unhaApodrecida >= 10 && $osso < 10 ) goto Osso
		if ( $ervaAzul >= 6 && $unhaApodrecida >= 10 && $osso >= 10 && $mandibula < 10 ) goto Mandibula


		:Erva
		log Pegar Erva
		[
		do conf lockMap cmd_fild02
		do mconf 1266 0 0 0
		do mconf 1073 0 0 0
		do mconf 1254 0 0 0
		do mconf Cornutus 0 0 0
		do mconf 1074 0 0 0
		do mconf 1391 0 0 0
		do conf attackAuto_inLockOnly 1
		call voltarAtacar
		]
		stop


		:Unha
		log Pegar Unha
		[
		do conf lockMap pay_dun00
		do mconf 1005 0 0 0
		do mconf 1031 0 0 0
		do mconf 1078 0 0 0
		do mconf 1084 0 0 0
		do conf attackAuto_inLockOnly 1
		call voltarAtacar
		]
		stop


		:Osso
		log Pegar Osso
		[
		do conf lockMap pay_dun01
		do mconf Drainliar 0 0 0
		do mconf Eggyra 0 0 0
		do mconf 1084 0 0 0
		do mconf 1078 0 0 0
		do conf attackAuto_inLockOnly 1
		do conf route_randomWalk 1
		call voltarAtacar
		]
		stop


		:Mandibula
		log Pegar mandibula
		[
		do conf lockMap pay_dun00
		do mconf 1005 0 0 0
		do mconf 1031 1 0 0
		do mconf 1078 0 0 0
		do mconf 1084 0 0 0
		do mconf 1076 0 0 0
		do conf attackAuto_inLockOnly 1
		do conf route_randomWalk 1
		call voltarAtacar
		]
		stop
	}
}

automacro tenhoOsItensIrProNpc {
	InInventory "Erva Azul" >= 6
	InInventory "Unha Apodrecida" >= 10
	InInventory "Osso" >= 10
	InInventory "Mandíbula Horrenda" >= 10
	QuestActive 2018
	Zeny > 9999
	NpcNotNear /Smith/
	exclusive 1
	call {
		lock virarArruaceiro3
		do conf -f questArruaceiro peguei
		log Vamos No NPC já temos os Itens...
		call pararDeAtacar
		do conf lockMap none
		do conf attackAuto_inLockOnly 0
		do mconf 1266 1 0 0
		do mconf 1073 1 0 0
		do mconf 1254 1 0 0
		do mconf Cornutus 1 0 0
		do mconf 1074 1 0 0
		do mconf 1391 1 0 0
		do mconf 1005 1 0 0
		do mconf 1031 1 0 0
		do mconf 1078 1 0 0
		do mconf 1084 1 0 0
		do mconf 1076 1 0 0
		lock virarArruaceiro3
		do move in_rogue &rand(370,373) &rand(20,24)
	}
}

automacro tenhoOsItensbugged {
	InInventory "Erva Azul" >= 6
	InInventory "Unha Apodrecida" >= 10
	InInventory "Osso" >= 10
	InInventory "Mandíbula Horrenda" >= 10
	QuestActive 2018
	Zeny < 10000
	exclusive 1
	call {
		log por algum motivo, o bot não tem nem os 10k de zeny pra fazer a quest
		log tentando resolver isso
		do autosell
	}
}


automacro tenhoOsItens {
	InInventory "Erva Azul" >= 6
	InInventory "Unha Apodrecida" >= 10
	InInventory "Osso" >= 10
	NpcNear /Smith/
	InInventory "Mandíbula Horrenda" >= 10
	Zeny > 9999
	QuestActive 2018
	exclusive 1
	call {
		lock virarArruaceiro3
		do conf lockMap none
		call pararDeAtacar
		do talk $.NpcNearLastBinId #Smith
		do talk $.NpcNearLastBinId #Smith
		do talk $.NpcNearLastBinId #Smith
		lock virarArruaceiro3
	}
}

automacro virarArruaceiro4_opção1 {
	JobID 6 #gatuno
	QuestActive 2022
	exclusive 1
	JobLevel = 50
	call {
		log sua posição atual é: $.map $.pos
		if ($.map = in_rogue && $.pos ~ 169 34, 246 25, 164 106) {
		[
		log ==================================
		log essa macro está se ativando no momento errado
		log reporte esse bug ao criador da macro
		log ==================================
		]
		stop
		}
		do conf attackAuto -1
		do conf -f questArruaceiro 1
		do move cmd_fild07
		do move cmd_fild09 107 195
		do talk resp 1
		do talk resp 2
		do talk resp 1
		do talk resp 0
	}
}

automacro virarArruaceiro4_opção2 {
	JobID 6 #gatuno
	QuestActive 2023
	exclusive 1
	JobLevel = 50
	call {
		log sua posição atual é: $.map $.pos
		if ($.map = in_rogue && $.pos ~ 169 34, 246 25, 164 106) {
		[
		log ==================================
		log essa macro está se ativando no momento errado
		log reporte esse bug ao criador da macro
		log ==================================
		]
		stop
		}
		do conf attackAuto -1
		do conf -f questArruaceiro 2
		do move cmd_fild04 304 180
		do talk resp 2
		do talk resp 1
		do talk resp 1
		do talk resp 2
	}
}

automacro virarArruaceiro4_opção3 {
	QuestActive 2024
	exclusive 1
	JobLevel = 50
	call {
		log sua posição atual é: $.map $.pos
		if ($.map = in_rogue && $.pos ~ 169 34, 246 25, 164 106) {
		[
		log ==================================
		log essa macro está se ativando no momento errado
		log reporte esse bug ao criador da macro
		log ==================================
		]
		stop
		}
		do conf attackAuto -1
		do conf -f questArruaceiro 3
		do move cmd_fild09 335 143
		do talk resp 0
		do talk resp 2
		do talk resp 4
		do talk resp 0
	}
}

automacro virarArruaceiro_morreuNoLabirinto_opção1 {
	QuestActive 2026
	ConfigKey questArruaceiro 1
	JobLevel = 50
	NotInMap in_rogue
	CurrentHP > 80%
	call {
		do conf attackAuto -1
		do move cmd_fild09 107 195
		do talk resp 1
		do talk resp 2
		do talk resp 1
		do talk resp 0
	}
}

automacro virarArruaceiro5_morreuNoLabirinto_opção2 {
	ConfigKey questArruaceiro 2
	QuestActive 2026
	JobLevel = 50
	NotInMap in_rogue
	CurrentHP > 80%
	exclusive 1
	call {
		do conf attackAuto -1
		do move cmd_fild04 304 180
		do talk resp 2
		do talk resp 1
		do talk resp 1
		do talk resp 2
	}
}

automacro virarArruaceiro5_morreuNoLabirinto_opção3 {
	ConfigKey questArruaceiro 3
	QuestActive 2026
	JobLevel = 50
	NotInMap in_rogue
	CurrentHP > 80%
	call {
		do conf attackAuto -1
		do move cmd_fild09 335 143
		do talk resp 0
		do talk resp 2
		do talk resp 4
		do talk resp 0
	}
}

automacro virarArruaceiro5buggeed {
	QuestActive 2026
	JobLevel = 50
	NotInMap in_rogue
	CurrentHP < 80%
	exclusive 1
	timeout 120
	call {
		[
		log ====================================
		log MORRI TENTANDO PASSAR PELO LABIRINTO
		log HP ATUAL: $.CurrentHPLastPercent \%
		log ESPRANDO HP FICAR ACIMA DE 80\%
		log ====================================
		]
		do sit
		if ( &config (sitAuto_hp_upper) != 80 ) {
		do conf sitAuto_hp_lower 20
		do conf sitAuto_hp_upper 80
		}
	}
}

automacro virarArruaceiro6.0 {
	JobLevel = 50
	IsInMapAndCoordinate in_rogue 169 34
	exclusive 1
	priority -5
	call {
		#essa é a parte depois que fala a senha na porta e entra na casa
		do talknpc 160 34 c r0 c
	}
}

automacro virarArruaceiro6.1 {
	JobLevel = 50
	IsInMapAndCoordinate in_rogue 246 25
	exclusive 1
	priority -5
	call {
		#essa é a parte depois que fala a senha na porta e entra na casa
		do move in_rogue 244 33
		do talk &npc (244 39)
		do talk resp 0
		#do talknpc 244 39 c r0 c
	}
}


automacro virarArruaceiro6.2 {
	JobLevel = 50
	IsInMapAndCoordinate in_rogue 164 106
	exclusive 1
	priority -5
	call {
		#essa é a parte depois que fala a senha na porta e entra na casa
		do move in_rogue 172 108
		do talknpc 177 109 c r0 c
	}
}

automacro virarArruaceiro7 {
	JobLevel = 50
	exclusive 1
	IsInMapAndCoordinate in_rogue 15 105
	macro_delay 0.5
	call {
		#ahhhh o labirinto.... o bixin vai morrer algumas vezes aqui viu... ou não kkkk
		do conf attackAuto -1
		do conf itemsTakeAuto 0
		log ================================
		log Iniciando Labirindo
		log ================================
		do move 23 105
		do move 23 334
		#preguiça de fazer o resto das coordenadas específicas
		do move in_rogue 359 117
	}
}

automacro virarArruaceiro8 {
	JobLevel = 50
	#IsInMapAndCoordinate in_rogue 359 117
	NpcNear /Marybell/
	InMap in_rogue
	QuestActive 2026
	exclusive 1
	call {
		log ===========================
		log PASSEI PELO LABIRINTO!!!!
		log ===========================
		do move in_rogue 369 119
		do talknpc 363 122 c c c
		do conf itemsTakeAuto 1
	}
}

automacro virarArruaceiro8Alternativo {
	JobLevel = 50
	#IsInMapAndCoordinate in_rogue 378 113
	NpcNear /Homem Assustador/
	InMap in_rogue
	QuestActive 2026
	exclusive 1
	call {
		log ===========================
		log PASSEI PELO LABIRINTO!!!!
		log ===========================
		do move in_rogue 369 119
		do talknpc 363 122 c c c
		do conf itemsTakeAuto 1
	}
}

automacro jaSouArruaceiro {
	JobID 17 #arruaceiro
	ConfigKey virarArruaceiro true
	exclusive 1
	call {
		do conf skillsAddAuto 1
		do conf skillsAddAuto_list RG_SNATCHER 10, RG_STEALCOIN 10, RG_BACKSTAP 4, RG_TUNNELDRIVE 5, RG_RAID 5, RG_INTIMIDATE 5, RG_PLAGIARISM 10
		do conf teleportAuto_MaxDmg 1000
		do iconf Erva Azul 0 1 0
		do iconf Mandíbula Horrenda 0 0 1
		do iconf Osso 0 0 1
		do iconf Unha Apodrecida 0 0 1
		$check = GetIndexAndEquipped("robe", 2560) #Capa Valhalla
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("topHead", 5583) #Chapeu valhalla
		if ($check != -1) do eq $check
		call pararDeAtacar
		do conf virarArruaceiro none
		do conf questArruaceiro none
	}
}
#
#
#
# FIM DA QUEST PRA VIRAR ARRUACEIRO
#

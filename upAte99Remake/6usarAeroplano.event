#Digite no console do eventMacro junopra --cidade
#Pode ser tambem einbrochpra
#ou rachelpra
#se o nome estiver certo, ele vai mover pra cidade desejada
macro junopra {
	log Vamos ir de Juno para $.param[0]
	#Se a cidade for izlude ou rachel , ele pega o aeroplano respectivo
	switch ( $.param[0] ) {
		case (=~ /izlude/i) {
			do conf -f aeroplano1 izlude
			do conf -f aeroplano2 none
		}
		case (=~ /rachel/i){
			do conf -f aeroplano1 rachel
			do conf -f aeroplano2 none
		}
		case (=~ /hugel/i) {
			do conf -f aeroplano1 none
			do conf -f aeroplano2 hugel
		}
		case (=~ /lighthalzen/i) {
			do conf -f aeroplano1 none
			do conf -f aeroplano2 lighthalzen
		}
		case (=~ /einbroch/i) {
			do conf -f aeroplano1 none
			do conf -f aeroplano2 eibroch
		}
		case (=~ /[jy]uno/) {
			log VOCÊ QUER IR DE JUNO PRA JUNO?
			log PORQUE VOCÊ FARIA ISSO?
		} else {
			[
			log Você digitou um nome inválido, por favor tente novamente
			log Os valores podem ser:
			log izlude , rachel , juno ou yuno
			log hugel , lighthalzen ou einbroch
			log Escreva como quiser, desde que escreva certo
			]
			stop
		}
	}
	$aeroplano1 = &config(aeroplano1)
	$aeroplano2 = &config(aeroplano2)
	if ($.map = airplane || $.map = airplane_01) {
	log ja estamos no aeroplano, só esperar agora
	} else {
		if ( $.map != yuno ) do move yuno
	}
	log $aeroplano1, $aeroplano2
}

macro einbrochpra {
	log Vamos ir de Eibroch para $.param[0]
	#se a cidade for juno hugel ou lighthalzen ele só pega 1 aeroplano
	# se for outra cidade, ele pega os dois aeroplanos
	switch ($.param[0]) {
		case (=~ /izlude/i) {
			do conf -f aeroplano1 izlude
			do conf -f aeroplano2 juno
		}
		case (=~ /rachel/i) {
			do conf -f aeroplano1 rachel
			do conf -f aeroplano2 juno
		}
		case (=~ /hugel/i) {
			do conf -f aeroplano1 none
			do conf -f aeroplano2 hugel
		}
		case (=~ /lighthalzen/i) {
			do conf -f aeroplano1 none
			do conf -f aeroplano2 lighthalzen
		}
		case (=~ /einbroch/i) {
			log VOCÊ QUER IR DE EINBROCH PRA EINBROCH?
			log PORQUE VOCÊ FARIA ISSO?
		}
		case (=~ /[jy]uno/) {
			do conf -f aeroplano1 none
			do conf -f aeroplano2 juno
		}
		else {
			[
			log Você digitou um nome inválido, por favor tente novamente
			log Os valores podem ser izlude , rachel , juno ou yuno ,
			log hugel , lighthalzen ou einbroch
			log pode ser com letra maiuscula ou sem
			]
			stop
		}
	}
	$aeroplano1 = &config(aeroplano1)
	$aeroplano2 = &config(aeroplano2)
	if ($.map = airplane || $.map = airplane_01) {
	log ja estamos no aeroplano, só esperar agora
	} else {
		do move airplane
	}
	log $aeroplano1, $aeroplano2
}

macro rachelpra {
	log Vamos ir de Rachel para $.param[0]
	#se a cidade for izlude ou juno ele só pega 1 aeroplano
	# se for outra cidade, ele pega os dois aeroplanos
	if ( $.param[0] =~ /izlude/i ) {
		do conf -f aeroplano1 izlude
		do conf -f aeroplano2 none
	} elsif ($.param[0] =~ /rachel/i ) {
		log VOCÊ QUER IR DE RACHEL PRA RACHEL?
		log PORQUE VOCÊ FARIA ISSO?
	} elsif ( $.param[0] =~ /hugel/i ) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 hugel
	} elsif ( $.param[0] =~ /lighthalzen/i ) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 lighthalzen
	} elsif ($.param[0] =~ /einbroch/i ) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 eibroch
	} elsif ( $.param[0] =~ /[jy]uno/) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 none
	} else {
		[
		log Você digitou um nome inválido, por favor tente novamente
		log Os valores podem ser izlude , rachel , juno ou yuno ,
		log hugel , lighthalzen ou einbroch
		log pode ser com letra maiuscula ou sem
		]
		stop
	}
	$aeroplano1 = &config(aeroplano1)
	$aeroplano2 = &config(aeroplano2)
	if ( $.map = airplane || $.map = airplane_01) {
	log ja estamos no aeroplano, só esperar agora
	} else {
		do move ra_fild12 294 208
		do talk resp 0
		log $aeroplano1, $aeroplano2
	}
}
automacro dentroDoAeroplanoBugged {
	InMap airplane, airplane_01
	exclusive 1
	ConfigKeyNot lockMap none
	call {
		do conf lockMap none
	}
}

automacro dentroDoAeroplanoInternacionalVarNaoDefinida {
	ConfigKeyNot aeroplano1 none
	InMap airplane_01
	timeout 50
	call {
		if (&defined($aeroplano1)) {
			$aeroplano1 = &config(aeroplano1)
		}
		log Estamos no aeroplano, aguardando chegada ao destino
	}
}

automacro dentroDoAeroplanoInternacional {
	InMap airplane_01
	LocalMsg /Bem-vindo a $aeroplano1|Welcome to $aeroplano1/i
	exclusive 1
	call sairDoAeroplano
}

automacro dentroDoAeroplanoDomesticoVarNaoDefinida {
	ConfigKeyNot aeroplano2 none
	InMap airplane
	timeout 50
	call {
		if ($aeroplano2 = undef) {
			$aeroplano2 = &config(aeroplano2)
		}
		log Estamos no aeroplano, aguardando chegada ao destino
	}
}

automacro dentroDoAeroplanoDomestico {
	InMap airplane
	LocalMsg /Bem-vindos a $aeroplano2|Welcome to $aeroplano2/i
	exclusive 1
	call sairDoAeroplano
}

macro sairDoAeroplano {
	do move 243 74
}

#Se estivermos na cidade, e o local desejado for essa cidade, ele completa o objetivo
#se estivermos na cidade, mas nao for o objetivo, nada acontece, já que provavelmente
#ele só está se locomovendo pra fazer qualquer outra coisa aleatoria que nao tem nada
# a ver com esse macro
automacro chegueiEmEinbroch {
	InMap einbroch
	ConfigKey aeroplano2 einbroch
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmHugel {
	InMap hugel
	ConfigKey aeroplano2 hugel
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmIzlude {
	InMap izlude
	ConfigKey aeroplano1 izlude
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmLighthalzen {
	InMap lighthalzen
	ConfigKey aeroplano2 lighthalzen
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmRachel {
	InMap ra_fild12
	ConfigKey aeroplano1 rachel
	run-once 1
	exclusive 1
	call {
		do move rachel
		call objetivoCompleto
	}
}

macro objetivoCompleto {
	#Se estivermos na cidade, e o local desejado for essa cidade, ele completa o objetivo
	log cheguei em $.InMapLast , objetivo completo
	do conf aeroplano2 none
	do conf aeroplano1 none
}

automacro chegueiEmJuno {
	InMap yuno
	ConfigKeyNot aeroplano1 none, aeroplano2 none
	exclusive 1
	call {
		# é aqui que ocorre a mágica, quando chega em juno
		# ele detecta se precisa pegar outro aeroplano ou não
		# se ele precisar ele vai
		# se não precisar ele vai pra proximo da kafra e completa o objetivo
		log destino para aeroplano1 é: &config(aeroplano1)
		log destino para aeroplano2 é: &config(aeroplano2)
		if (&config(aeroplano1) =~ /rachel|izlude/i  ) {
		do move airplane_01
		} elsif ( &config(aeroplano2) =~ /hugel|einbroch|lighthalzen/i ) {
		do move airplane
		} elsif ( &config(aeroplano1) =~  /[yj]uno/i || &config(aeroplano2) =~ /[yj]uno/i ) {
			do move &rand(150, 154) &rand(182, 186)
		call objetivoCompleto
		} else {
			log Falha em descobrir a rota correta, revise o código
		}
	}
}
#
#
#
# FIM DA MACRO DE SALVAR NO AEROPLANO
#

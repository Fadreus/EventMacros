automacro EdenJoinMoveNpc {
	InInventoryID 6219 = 0
	InInventoryID 2414 = 0
	exclusive 1
	BaseLevel >= 12
	ConfigKeyNot membroDoEden sim
	NpcNotNear /Lenore|Lime/
	JobIDNot 0
	run-once 1
	call {
		do move moc_para01 &rand(33,35) &rand(25,27) #posição otima no Eden
	}
}

automacro EdenJoinTalkNpc {
	InInventory "Emblema do Grupo Valhalla" = 0
	ConfigKeyNot membroDoEden sim
	exclusive 1
	run-once 1
	NpcNear /Lenore|Lime/
	call {
		do talk $.NpcNearLastBinId
		do talk resp 1
		do talk resp 0
		do talk text lolol
		do talk text lolol
		do talk resp 4
		do iconf 6219 0 0 0
		do conf -f membroDoEden sim
	}
}

automacro EdenJoinedBugged {
	InInventoryID 6219 = 1
	ConfigKeyNotExist membroDoEden
	exclusive 1
	run-once 1
	call {
		do conf -f membroDoEden sim
	}
}
automacro iniciandoQuestEden12MoverPerto {
	NpcNotNear /Boya/
	InInventoryID 6219 = 1
	BaseLevel 12..18
	exclusive 1
	JobID 6 #gatuno
	QuestInactive 7128
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15009
	InInventoryID 15009 = 0
	call moverPertoDosNpcEden
}

automacro iniciandoQuestEden12 {
	NpcNear /Boya/
	InInventoryID 6219 = 1
	BaseLevel 12..18
	exclusive 1
	JobID 6 #gatuno
	QuestInactive 7128
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15009
	InInventoryID 15009 = 0
	call {
		#do talknpc 25 35 c r1 c c c c c r0 c r0 c c c c c c #Boya
		do talk $.NpcNearLastBinId
		do talk resp 1
		do talk resp 0
		do talk resp 0
	}
}

automacro iniciandoQuestEden26MoverPerto {
	NpcNotNear /Boya/
	BaseLevel 26..32
	InInventoryID 6219 = 1
	JobID 6 #gatuno
	exclusive 1
	QuestInactive 7138
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15010
	InInventoryID 15010 = 0
	call moverPertoDosNpcEden
}

automacro iniciandoQuestEden26 {
	NpcNear /Boya/
	BaseLevel 26..32
	InInventoryID 6219 = 1
	JobID 6 #gatuno
	exclusive 1
	QuestInactive 7138
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15010
	InInventoryID 15010 = 0
	call {
		#do talknpc 25 35 c c r1 c c c c c c #Boya
		do talk $.NpcNearLastBinId
		do talk resp 1
	}
}

automacro IniciandoQuestEden40MoverPerto {
	NpcNotNear /Boya/
	BaseLevel 40..49
	InInventoryID 6219 = 1
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	JobID 6 #gatuno
	exclusive 1
	QuestInactive 7147
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15011
	InInventoryID 15011 = 0
	call moverPertoDosNpcEden
}

automacro IniciandoQuestEden40 {
	NpcNear /Boya/
	BaseLevel 40..49
	InInventoryID 6219 = 1
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	JobID 6 #gatuno
	exclusive 1
	QuestInactive 7147
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15011
	InInventoryID 15011 = 0
	call {
		#do talknpc 25 35 c r0 c c c c c c #Boya
		do talk $.NpcNearLastBinId
		do talk resp 0
	}
}

automacro inciandoQuestEden60MoverPerto {
	NpcNotNear /Ur/
	BaseLevel 60..69
	exclusive 1
	InInventoryID 6219 = 1
	JobID 17 #arruaceiro
	QuestInactive 7214
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15031
	InInventoryID 15031 = 0
	call moverPertoDosNpcEden
}

automacro inciandoQuestEden60 {
	NpcNear /Ur/
	BaseLevel 60..69
	exclusive 1
	InInventoryID 6219 = 1
	JobID 17 #arruaceiro
	QuestInactive 7214
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	IsNotEquippedID armor 15031
	InInventoryID 15031 = 0
	call {
		#do talknpc 23 35 c r0 c c c c c r1 c c c c c c #Uru
		do talk $.NpcNearLastBinId
		do talk resp 0
		do talk resp 1
	}
}

macro moverPertoDosNpcEden {
	do conf lockMap none
	call pararDeAtacar
	do move moc_para01 &rand(33,35) &rand(25,27) #posição otima no Eden
}

automacro Eden12salvarNaKafra {
	QuestActive 7128
	exclusive 1
	BaseLevel 15..25
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot saveMap morocc
	call {
		do conf -f quest_eden em_curso
		call SetSaveIn "morocc"
	}
}

automacro Eden12IrNoMapa {
	QuestActive 7128
	exclusive 1
	BaseLevel 15..25
	ConfigKeyNot In_saveMap_sequence true
	ConfigKey saveMap morocc
	call {
		#eden 12
		do conf -f quest_eden em_curso
		log Indo Começar quest Eden 12
		do move moc_fild11 &rand(181,183) &rand(254,256)
		do talk 0 #cão falante
		do talk resp 1
		do mconf Filhote de Lobo 0 0 0
		do mconf Escorpião 0 0 0
		do mconf Condor 0 0 0
	}
}

automacro Eden26SalvarNaKafra {
	QuestActive 7138
	BaseLevel 30..39
	exclusive 1
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot saveMap payon
	call {
		do conf -f quest_eden em_curso
		call SetSaveIn "payon"
	}
}

automacro Eden26IrNoMapa {
	QuestActive 7138
	BaseLevel 30..39
	exclusive 1
	ConfigKeyNot In_saveMap_sequence true
	ConfigKey saveMap payon
	call {
		#eden 26
		do conf -f quest_eden em_curso
		log Indo Começar quest Eden 26
		do move pay_arche &rand(44,46) &rand(132,134)
		#do talknpc 41 136 c c c c c c c c #Coral
		do talk &npc( 41 136 )
		do mconf Esqueleto 0 0 0
		do mconf Poporing 0 0 0
		do mconf Zumbi 0 0 0
	}
}

automacro Eden40SalvarNaKafra {
	QuestActive 7147
	BaseLevel 49..59
	exclusive 1
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	ConfigKeyNot virarArrauceiro true
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot saveMap prontera
	call {
		do conf -f quest_eden em_curso
		call SetSaveIn "prontera"
	}
}

automacro Eden40IrNoMapa {
	QuestActive 7147
	BaseLevel 50..59
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	exclusive 1
	ConfigKeyNot virarArrauceiro true
	ConfigKeyNot In_saveMap_sequence true
	ConfigKey saveMap prontera
	call {
		#eden 40
		do conf -f quest_eden em_curso
		log Indo Começar quest Eden 40
		do move in_orcs01 &rand(34,36) &rand(162,164)
		do talk 0 #Absalon
		do mconf Guerreiro Orc 0 0 0
		do mconf Filhote de Orc 0 0 0
		do mconf Senhora Orc 0 0 0
	}	
}

automacro Eden60SalvarNaKafra {
	QuestActive 7214
	BaseLevel 69..79
	exclusive 1
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot saveMap comodo
	call {
		do conf -f quest_eden em_curso
		call SetSaveIn "comodo"
	}
}

automacro Eden60IrNoMapa {
	QuestActive 7214
	BaseLevel 69..79
	exclusive 1
	ConfigKeyNot In_saveMap_sequence true
	ConfigKey saveMap comodo
	call {
		#eden 60
		do conf -f quest_eden em_curso
		log Indo Começar quest Eden 60
		do move comodo &rand(174,176) &rand(344,346)
		#do talknpc 173 354 c c c c c #Mémbro do grupo Éden#2n
		do talk &npc( 173 354 ) #Mémbro do grupo Éden#2n
	}
}

automacro Eden12Caçar {
	QuestHuntOngoing 7129 1009, 7130 1107, 7131 1001
	exclusive 1
	timeout 120
	call {
		call caçarMonstros
		if ( &config(lockMap) != moc_fild11) do conf lockMap moc_fild11		
	}
}

automacro Eden26Caçar {
	QuestHuntOngoing 7139 1076, 7140 1031
	exclusive 1
	timeout 180
	call {
		call caçarMonstros
		if ( &config(lockMap) != pay_dun00) do conf lockMap pay_dun00
	}
}

automacro Eden40Caçar {
	QuestHuntOngoing 7148 1686, 7149 1023, 7150 1273
	exclusive 1
  	timeout 180
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	call {
		call caçarMonstros
		if ( &config(lockMap) != gef_fild10) do conf lockMap gef_fild10
	}
}

automacro Eden60Caçar {
	QuestHuntOngoing 7215 1278, 7216 1278
	exclusive 1
	timeout 60
	call {
		call caçarMonstros
		if ( &config(lockMap) != beach_dun2) do conf lockMap beach_dun2
	}
}

macro caçarMonstros {
	[
	call voltarAtacar
	do mconf $.QuestHuntOngoingLastMobID 2 0 0
	]
}

automacro Eden60JuntarFolhasEOmbreiras {
	QuestActive 7217
	exclusive 1
	timeout 120
	call {
		do iconf 7196 5 1 0 #ombreira
		do iconf 7100 7 1 0 #folha
		$qtdDeOmbeira = GetNamenyNameID(7196)
		$qtdDeFolha = GetNamenyNameID(7100)
		$ombreira = &invamount ( $qtdDeOmbeira )
		$folha = &invamount ( $qtdDeFolha )
		if ( $ombreira < 5 )                 goto Ombreira
		if ( $ombreira >= 5 && $folha < 7 )  goto Folha
		if ( $ombreira >= 5 && $folha >= 7 ) goto Juntei
		
		:Ombreira
		log Pegar Ombreiras
		do conf lockMap um_fild02
		call voltarAtacar
		stop
		
		:Folha
		log Pegar Folhas
		do conf lockMap um_fild01
		call voltarAtacar
		stop
		
		:Juntei
		log Vamos No NPC já temos os Itens.
		call pararDeAtacar
		do move um_fild01 &rand(36,38) &rand(277,279)
		do talknpc 34 280 c  #Romeo#2nd02
		stop
	}
}

automacro Eden12FinalizarCaças {
	QuestHuntCompleted 7129 1009, 7130 1107, 7131 1001
	exclusive 1
	call {
		call pararDeAtacar
		do mconf $.QuestHuntCompletedLastMobID 0 0 0
		do move moc_fild11 &rand(181,183) &rand(254,256)
	  	do talk 0 #cao falante 
		release Eden12Caçar
	}
}

automacro Eden26FinalizarCaças {
	QuestHuntCompleted 7139 1076, 7140 1031
	exclusive 1
	call {
		call pararDeAtacar
		do mconf $.QuestHuntCompletedLastMobID 0 0 0
		do move pay_arche &rand(44,46) &rand(132,134)
		#do talknpc 41 136 c c c c c #Coral
		do talk &npc( 41 136 ) #Coral 
		release Eden26Caçar
	}
}

automacro Eden40FinalizarCaças {
	QuestHuntCompleted 7148 1686, 7149 1023, 7150 1273
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	exclusive 1
	call {
		call pararDeAtacar
		do mconf $.QuestHuntCompletedLastMobID 0 0 0
		do move in_orcs01 &rand(34,36) &rand(162,164)
		#do talknpc 38 175 c c #Absalom#para07
		do talk 0 #Absalom
		release Eden40Caçar
	}
}

automacro Eden60FinalizarCaças {
	QuestHuntCompleted 7215 1278, 7216 1278
	exclusive 1
	JobID 17 #arruaceiro
	call {
		do mconf $.QuestHuntCompletedLastMobID 0 0 0
		switch ( $.QuestHuntCompletedLastQuestID ) {
			
			case (= 7215) { #golem1
				do move comodo &rand(174,176) &rand(344,346)
				#do talknpc 173 354 c c c c c #Mémbro do grupo Éden#2n
				do talk &npc( 173 354 )
				release Eden60Caçar
			}
			
			case (= 7216) { #golem2
				do move um_fild01 &rand(36,38) &rand(277,279)
				#do talknpc 34 280 c c c c #Romeo#2nd02
				do talk &npc( 34 280 )
				release Eden60Caçar
			}
		}
	}
}

automacro voltarDoMapa {
	QuestActive 7132, 7141, 7151, 7218
	NpcNotNear /Boya|Ur/
	exclusive 1
	call {
		log Completei as Caças!
		log vamo lá pegar meus equips de direito!
		call moverPertoDosNpcEden
	}
}

automacro voltarDoMapaDentroDoEden {
	QuestActive 7132, 7141, 7151, 7218
	NpcNear /Boya|Ur/
	exclusive 1
	call {
		#7132 : eden 12
		#7141 : eden 26
		#7151 : eden 40
		#7218 : eden 60
		switch ($.QuestActiveLastID) {
			
			case (= 7132) { #eden 12
				#do talknpc 25 35 c c c #Boya
				do talk &npc(/Boya/)
				do mconf Filhote de Lobo 1 0 0
				do mconf Escorpião 1 0 0
				do mconf Condor 1 0 0
				release EdenPegandoEquips12e26e40
			}
			
			case (= 7141) { #eden 26
				#do talknpc 25 35 c c #Boya
				do talk &npc(/Boya/)
				do mconf Esqueleto 1 0 0
				do mconf Poporing 1 0 0
				do mconf Zumbi 1 0 0
				release EdenPegandoEquips12e26e40
			}
			
			case (= 7151) { #eden 40
				#do talknpc 25 35 c c #Boya
				do talk &npc(/Boya/)
				do mconf Guerreiro Orc 1 0 0
				do mconf Filhote de Orc 1 0 0
				do mconf Senhora Orc 1 0 0
				release EdenPegandoEquips12e26e40
			}
			
			case (= 7218) { #eden 60
				#do talknpc 23 35 c c c #Uru
				do talk &npc(/Ur/)
			}
		}
		do conf -f quest_eden terminando
	}
}
automacro EdenPegandoEquips12e26e40e60IndoProNpc {
	NpcNotNear /hael/i
	ConfigKey quest_eden terminando
	BaseLevel 12..99
	exclusive 1
	call {
		do move moc_para01 &rand(112,114) &rand(31,33)
	}
}

automacro EdenPegandoEquips12e26e40 {
	ConfigKey quest_eden terminando
	BaseLevel 12..59
	exclusive 1
	NpcNear /hael/i
	call {
		do talk $.NpcNearLastBinId
		do talk resp 0
		do talk resp 1
		
		if ( $.lvl >= 40 ) {
			#do talknpc 112 96 c r2 c c r4 c r0 c #Administrador Mihael(encanto de destreza do chapéu valhalla)
			do talk $.NpcNearLastBinId
			do talk resp 2
			do talk resp 4
			do talk resp 0
		}
		do conf quest_eden none
	}
}

automacro Eden12Equipando {
	IsNotEquippedID armor 15009
	InInventoryID 15009 = 1
	BaseLevel 12..25 
	exclusive 1
	run-once 1 
	call {
		$check = GetIndexAndEquipped("robe", 2560) #Capa Valhalla
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("topHead", 5583) #Chapeu valhalla
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("shoes", 2456) #Botas Valhalla Iniciante
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("armor", 15009) #Uniforme Valhalla Iniciante
		if ($check != -1) do eq $check
	}
}

automacro Eden26Equipando {
	IsNotEquippedID armor 15010
	InInventoryID 15010 = 1
	BaseLevel 26..39
	exclusive 1
	run-once 1
	call {
		$check = GetIndexAndEquipped("shoes", 2457) #Botas Valhalla Intermediárias
		if ($check != -1) do eq $check 
		$check = GetIndexAndEquipped("rightHand", 13050) #Adaga Valhalla Iniciante
		if ($check != -1) do eq $check 
		$check = GetIndexAndEquipped("armor", 15010) #Uniforme Valhalla Intermediário
		if ($check != -1) do eq $check 
	}
}

automacro Eden40Equipando {
	IsNotEquippedID armor 15011
	InInventoryID 15011 = 1
	BaseLevel 40..59
	exclusive 1
	run-once 1
	call {
		$check = GetIndexAndEquipped("shoes", 2458) #Botas Valhalla Avançadas
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("rightHand", 13051) #Adaga Valhalla Intermediária
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("armor", 15011) #Uniforme Valhalla Avançado
		if ($check != -1) do eq $check
	}
}

automacro EdenPegandoEquips60 {
	ConfigKey quest_eden terminando
	BaseLevel 60..98
	exclusive 1
	IsNotEquippedID armor 15031
	NpcNear /Ferreiro|smith/i
	call {
		#do talknpc 111 83 c r2 c c r1 #Ferreiro Torhen#2nd10
		do talk $.NpcNearLastBinId
		do talk resp 2
		do talk resp 1
		do conf quest_eden none
	}
}

automacro Eden60Equipando {
	IsNotEquippedID armor 15031
	InInventoryID 15031 = 1
	BaseLevel 60..98
	exclusive 1		
	call {
		$check = GetIndexAndEquipped("robe", 2571) #Capa II do Grupo Eden
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("topHead", 18514) #Chapéu_II_do_Grupo_Éden#
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("shoes", 2473) ##Botas_IV_do_Grupo_Éden#
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("rightHand",13066) #Adaga_III_do_Grupo_Éden#
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("armor", 15031) #Armadura_do_Grupo_Éden#
		if ($check != -1) do eq $check
	}
}
#
#
#
# FIM DAS QUESTS EDEN
#



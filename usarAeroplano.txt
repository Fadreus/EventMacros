macro yunopra {
	log $.param1
	#Se a cidade for izlude ou rachel , ele pega o aeroplano respectivo
	if ( $.map != yuno ) do move yuno
    if ( $.param1 =~ /izlude|rachel/i ) {
        [
		add_key("aeroplano1","$.param1")
		add_key("aeroplano2", "none")
        ]
		do move airplane_01
	#Se a cidade for einbroch,hugel,lighthalzen , ele pega o outro aeroplano 
    } elsif ( $.param1 =~ /einbroch|hugel|lighthalzen/i ) {
        add_key ("aeroplano1" , "none")
		add_key("aeroplano2","$.param1")
        do move airplane
    } else {
	[
	log Você digitou um nome inválido, por favor tente novamente
	log Os valores podem ser izlude , rachel , juno ou yuno , 
	log hugel , lighthalzen ou einbroch
	log pode ser com letra maiuscula ou sem
	]
	stop
	}
}

macro einbrochpra {
	#se a cidade for juno hugel ou lighthalzen ele só pega 1 aeroplano
	# se for outra cidade, ele pega os dois aeroplanos
	if ($.param1 =~ /izlude|rachel/i ) {
	add_key("aeroplano1","$.param1")
	add_key("aeroplano2", "yuno")
	} elsif ( $.param1 =~ /([yj]uno|hugel|lighthalzen)/i ) {
	add_key("aeroplano2","$.param1")
	add_key("aeroplano1", "none")
	} else {
	[
	log Você digitou um nome inválido, por favor tente novamente
	log Os valores podem ser izlude , rachel , juno ou yuno , 
	log hugel , lighthalzen ou einbroch
	log pode ser com letra maiuscula ou sem
	]
	stop
	}
	do move airplane
}

macro rachelpra {
	#se a cidade for izlude ou juno ele só pega 1 aeroplano
	# se for outra cidade, ele pega os dois aeroplanos
	do move ra_fild12 294 208
	do talk cont
	do talk resp 0
	if ( $.param1 =~ /izlude|[yj]uno/i ) {
	add_key("aeroplano1","$.param1")
	add_key("aeroplano2", "none")
	} elsif ( $.param1 =~ /einbroch|hugel|lighthalzen/i ) {
	add_key("aeroplano1","yuno")
	add_key("aeroplano2","$.param1")
	} else {
	[
	log Você digitou um nome inválido, por favor tente novamente
	log Os valores podem ser izlude , rachel , juno ou yuno , 
	log hugel , lighthalzen ou einbroch
	log pode ser com letra maiuscula ou sem
	]
	stop
	}
}

automacro dentroDoAeroplanoInternacional {
InMap airplane_01
LocalMsg /Bem-vindo(.*)|Welcome to (.*)/
exclusive 1
call {
	#detecta se é o aeroplano pousou na cidade desejada, se não for, ele nao faz nada
	if (&config(aeroplano1) =~ /izlude/i && $.LocalMsgLastMsg =~ /izlude/i ) {
	do move 243 74
	} elsif (&config(aeroplano1) =~ /rachel/i && $.LocalMsgLastMsg =~ /rachel/i ) {
	do move 243 74
	} elsif (&config(aeroplano1) =~ /[yj]uno/i && $.LocalMsgLastMsg =~ /juno/i) {
	do move 243 74
	} else {
	log não é o destino que quero, esperando... 
	}
}
}

automacro dentroDoAeroplanoDomestico {
InMap airplane
LocalMsg /Bem-vindo(.*)|Welcome to (.*)/
exclusive 1
call {
	#detecta se é o aeroplano pousou na cidade desejada, se não for, ele nao faz nada
	if (&config(aeroplano2) =~ /hugel/i && $.LocalMsgLastMsg =~ /Hugel/i ) {
	do move 243 74
	} elsif (&config(aeroplano2) =~ /lighthalzen/i && $.LocalMsgLastMsg =~ /lighthalzen/i ) {
	do move 243 74
	} elsif (&config(aeroplano2) =~ /einbroch/i && $.LocalMsgLastMsg =~ /einbroch/i ) {
	do move 243 74
	} elsif (&config(aeroplano2) =~ /[yj]uno/i && $.LocalMsgLastMsg =~ /juno/i) {
	do move 243 74
	} else {
	log não é o destino que quero, esperando... 
	}
	}
}

#Se estivermos na cidade, e o local desejado for essa cidade, ele completa o objetivo
#se estivermos na cidade, mas nao for o objetivo, nada acontece, já que provavelmente
#ele só está se locomovendo pra fazer qualquer outra coisa aleatoria que nao tem nada
# a ver com esse macro
automacro chegueiEmEinbroch {
InMap einbroch
ConfigKey aeroplano2 einbroch, aeroplano2 Einbroch, aeroplano2 EINBROCH
run-once 1
exclusive 1
call objetivoCompleto
}

automacro chegueiEmHugel { 
InMap hugel
ConfigKey aeroplano2 hugel aeroplano2 Hugel, aeroplano2 HUGEL
run-once 1
exclusive 1
call objetivoCompleto
}

automacro chegueiEmIzlude {
InMap izlude
ConfigKey aeroplano1 izlude, aeroplano1 Izlude, aeroplano1 IZLUDE
run-once 1
exclusive 1
call objetivoCompleto
}

automacro chegueiEmLighthalzen {
InMap lighthalzen
ConfigKey aeroplano2 lighthalzen, aeroplano2 Lighthalzen, aeroplano2 LIGHTHALZEN
run-once 1
exclusive 1
call objetivoCompleto
}

automacro chegueiEmRachel {
InMap ra_fild12
ConfigKey aeroplano1 rachel, aeroplano1 Rachel, aeroplano1 RACHEL
run-once 1
exclusive 1
call {
	call objetivoCompleto
	do move rachel
}
}

macro objetivoCompleto {
	#Se estivermos na cidade, e o local desejado for essa cidade, ele completa o objetivo 
	log cheguei em $.InMapLast , objetivo completo
	do conf aeroplano2 none
	do conf aeroplano1 none
}

automacro chegueiEmJuno {
InMap yuno
ConfigKeyNot aeroplano1 none, aeroplano2 none
exclusive 1
call {
	# é aqui que ocorre a mágica, quando chega em juno
	# ele detecta se precisa pegar outro aeroplano ou não
	# se ele precisar ele vai
	# se não precisar ele vai pra proximo da kafra e completa o objetivo
	log destino para aeroplano1 é: &config(aeroplano1)
	log destino para aeroplano2 é: &config(aeroplano2)
	if (&config(aeroplano1) =~ /rachel|izlude/i  ) {
	do move airplane_01
	} elsif ( &config(aeroplano2) =~ /hugel|einbroch|lighthalzen/i ) {
	do move airplane
	} elsif ( &config(aeroplano1) =~  /[yj]uno/i || &config(aeroplano2) =~ /[yj]uno/i ) {
	do move &rand(150, 154) &rand(182, 186)
	call objetivoCompleto
	} else {
	log Tem algo errado, provavelmente um bug
	}
}
}

sub add_key {
	my ($key, $value) = @_;
	configModify($key, $value);
}
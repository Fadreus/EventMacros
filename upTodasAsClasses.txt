#campo dos aprendizes by HenryBK and Nipodemos

automacro moveToSprakkiOutside {
	exclusive 1
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	BaseLevel = 1
	NpcNotNear /Sprakki/
	QuestInactive 7117
	call {
		do move 52 113
	}
}

automacro TalkSprakkiOutside {
	exclusive 1
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	BaseLevel = 1
	NpcNear /Sprakki/
	QuestInactive 7117
	call {
		[
		do conf autoMoveOnDeath 1
		do conf autoMoveOnDeath_x none
		do conf autoMoveOnDeath_y none
		do conf autoMoveOnDeath_map none
		do conf saveMap none
		do conf saveMap_warpToBuyOrSell 0
		do conf sellAuto 0
		do conf storageAuto 0
		do conf storageAuto_npc none
		do conf getAuto_0 none
		do conf autoTalkCont 1
		do conf sitAuto_idle 0
		do conf sitAuto_hp_lower 40
		do conf sitAuto_hp_upper 80
		do conf itemsTakeAuto 0
		do conf itemsGatherAuto 0
		do conf lockMap none
		do conf attackAuto_inLockOnly 0
		do conf route_randomWalk 0
		do conf statsAddAuto 1
		do conf statsAddAuto_list 10 dex, 15 agi, 20 dex, 25 agi, 20 str, 30 dex, 60 agi, 30 str, 40 dex, 20 vit, 80 agi, 25 luk ,60 dex, 50 str, 40 luk, 90 agi, 60 str, 29 vit
		do conf statsAddAuto_dontUseBonus 1
		do conf skillsAddAuto 1
		do conf skillsAddAuto_list NV_BASIC 9, TF_DOUBLE 10, TF_MISS 10, TF_STEAL 10, TF_HIDING 8, TF_POISON 10, TF_DETOXIFY 1, RG_SNATCHER 10, RG_STEALCOIN 10, RG_BACKSTAP 4, RG_TUNNELDRIVE 5, RG_RAID 5, RG_INTIMIDATE 5, RG_PLAGIARISM 10
		do conf itemsMaxWeight 49
		do conf itemsMaxWeight_sellOrStore 48
		do conf itemsMaxNum_sellOrStore 99
		do iconf 12323 0 0 0
		do iconf 12324 0 0 0
		do iconf 569 0 0 0
		do iconf 7059 50 0 0
		do iconf 7060 50 0 0
		do iconf 13040 0 0 0
		do iconf 1243 0 0 0
		do iconf 2112 0 0 0
		do iconf 5055 0 0 0
		do iconf 2414 0 0 0
		do iconf 2510 0 0 0
		do iconf 2352 0 0 0
		do iconf 1201 0 0 0
		do iconf 13041 0 0 0
		do iconf 2393 0 0 0
		do iconf 2301 0 0 0
		]
		do talk $.NpcNearLastBinId
	}
}

automacro moveInside {
	exclusive 1
	QuestActive 7117
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	priority 1
	call GotoInside
}

automacro moveInsideBugged1 {
	exclusive 1
	QuestInactive 7117
	BaseLevel = 2
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	priority 1
	call GotoInside
}

automacro moveInsideBugged2 {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	InMap new_1-1, new_2-1, new_3-1, new_4-1, new_5-1
	priority 1
	call GotoInside
}

macro GotoInside {
	$nextMap = nextMap("$.map")
	do move $nextMap 101 29
}

automacro moveNextToSprakki {
	exclusive 1
	QuestActive 7117
	NpcNotNear /Sprakki/
	BaseLevel = 1
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call GotoSpraki
}

automacro moveNextToSprakkiBugged1 {
	exclusive 1
	QuestInactive 7117
	BaseLevel = 2
	NpcNotNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call GotoSpraki
}

automacro moveNextToSprakkiBugged2 {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	NpcNotNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call GotoSpraki
}

macro GotoSpraki {
	do move 101 29
}

automacro talkSprakkiBugged1 {
	exclusive 1
	QuestInactive 7117
	BaseLevel = 2
	NpcNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call SprakiiBuggedTalk
}

automacro talkSprakkiBugged2 {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	NpcNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call SprakiiBuggedTalk
}

macro SprakiiBuggedTalk {
	do talk $.NpcNearLastBinId
	
}

automacro talkSprakki {
	exclusive 1
	QuestActive 7117
	BaseLevel = 1
	NpcNear /Sprakki/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call {
		do talk $.NpcNearLastBinId
		do talk resp 0
	}
}

automacro moveNextToBrade1 {
	exclusive 1
	QuestActive 7118
	NpcNotNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 2
	call MoveToBrade
}

automacro moveNextToBrade2 {
	exclusive 1
	QuestActive 7119
	NpcNotNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 2
	call MoveToBrade
}

macro MoveToBrade {
	do move 103 105
}

automacro talkBrade {
	exclusive 1
	QuestActive 7118
	BaseLevel = 2
	NpcNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call {
		do talk $.NpcNearLastBinId
	}
}


automacro moveNextToBradeBugged {
	exclusive 1
	QuestInactive 7118
	QuestInactive 7119
	BaseLevel = 3
	InInventoryID 5055 = 0
	NpcNotNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 1
	call MoveToBrade
}

automacro talkBradeBugged {
	exclusive 1
	QuestInactive 7118
	QuestInactive 7119
	BaseLevel = 3
	InInventoryID 5055 = 0
	NpcNear /Brade/
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	priority 0
	call {
		do talk $.NpcNearLastBinId
	}
}

automacro equipStuffForBrade {
	exclusive 1
	QuestActive 7119
	IsNotEquippedID topHead 5055, leftHand 2112, robe 2510, shoes 2414, armor 2352, rightHand 1243
	call {
		$check = GetIndexAndEquipped("topHead", 5055)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("robe", 2510)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("shoes",2414)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("armor",2352)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("leftHand",2112)
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("rightHand",1243)
		if ($check != -1) do eq $check
	}
}

automacro talkBradeSecond {
	exclusive 1
	QuestActive 7119
	BaseLevel = 3
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	NpcNear /Brade/
	call {
		do talk $.NpcNearLastBinId
	}
}

automacro BradeBuggedNo7120MoveJinha {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 0
	NpcNotNear /Jinha/
	call MoveJinha
}

automacro moveToGirlSkill {
	exclusive 1
	QuestActive 7120
	NpcNotNear /Jinha/
	SkillLevel NV_FIRSTAID = 0
	priority 0
	call MoveJinha
}

macro MoveJinha {
	do move 107 108
}

automacro BradeBuggedNo7120TalkJinha {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 0
	NpcNear /Jinha/
	call TalkJinha
}

automacro talkGirlSkill {
	exclusive 1
	QuestActive 7120
	NpcNear /Jinha/
	SkillLevel NV_FIRSTAID = 0
	priority 0
	call TalkJinha
}

macro TalkJinha {
	do talk $.NpcNearLastBinId
	do talk resp 0
}

automacro BradeBuggedNo7120MoveBrade {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	InInventoryID 2393 = 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 1
	NpcNotNear /Brade/
	call MoveToBrade
}

automacro BradeBuggedNo7120TalkBrade {
	exclusive 1
	QuestInactive 7119
	QuestInactive 7120
	BaseLevel = 4
	InInventoryID 12324 > 0
	InInventoryID 2393 = 0
	IsEquippedID topHead 5055
	IsEquippedID leftHand 2112
	IsEquippedID rightHand 1243
	IsEquippedID robe 2510
	IsEquippedID armor 2352
	IsEquippedID shoes 2414
	SkillLevel NV_FIRSTAID = 1
	NpcNear /Brade/
	call TalkBradeGetTunic
}

automacro talkBradeThird {
	exclusive 1
	QuestActive 7120
	SkillLevel NV_FIRSTAID = 1
	NpcNear /Brade/
	priority 0
	call TalkBradeGetTunic
}

macro TalkBradeGetTunic {
	do talk $.NpcNearLastBinId
}

automacro equipStuffForGirl {
	exclusive 1
	QuestInactive 7120
	InInventoryID 2393 = 1
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	IsNotEquippedID armor 2393
	call {
		$check = GetIndexAndEquipped("armor", 2393)
		if ($check != -1) {
			do eq $check
		}
	}
}

automacro moveNextToChoco {
	exclusive 1
	IsEquippedID armor 2393
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	NpcNotNear /Choco/
	priority 1
	call {
		do move 32 171
	}
}

automacro talkChoco {
	exclusive 1
	QuestInactive 7120
	QuestInactive 7121
	IsEquippedID armor 2393
	InMap new_1-2, new_2-2, new_3-2, new_4-2, new_5-2
	NpcNear /Choco/
	call {
		do talk $.NpcNearLastBinId
		do conf -f  current_npc kafra
	}
}

automacro moveNextToGuys {
	exclusive 1
	QuestActive 7121
	NpcNotNear /(Choco|Kafra|Zonda|Soldado)/
	priority 0
	call {
		do move 32 171
	}
}
automacro talkToGuysBugged {
	exclusive 1
	QuestActive 7121
	ConfigKeyNotExist current_npc
	NpcNear /Kafra/
	call {
		log Há um bug, a config $.ConfigKeyNotExistLastKey já deveria existir nesse ponto
		log Mas ainda não existe
		log Mudando isso:
		do conf -f current_npc kafra
	}
}

automacro talkKafra {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc kafra
	NpcNear /Kafra/
	call {
		do talk $.NpcNearLastBinId
		do talk resp 4
		do conf current_npc zonda
	}
}

automacro talkZonda {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc zonda
	NpcNear /Zonda|Corp/
	call {
		do talk $.NpcNearLastBinId
		do conf current_npc soldado
	}
}

automacro talkSoldado {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc soldado
	NpcNear /Soldado|Guide/
	call {
		do talk $.NpcNearLastBinId
		do conf current_npc choco
	}
}

automacro talkChocoAgain {
	exclusive 1
	QuestActive 7121
	ConfigKey current_npc choco
	NpcNear /Choco/
	call {
		do talk $.NpcNearLastBinId
		do conf current_npc kafra
		
	}
}

automacro moveBradeGrounds {
	exclusive 1
	QuestInactive 7122
	InInventoryID 13040 = 0
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	NpcNotNear /Brade/
	priority 1
	call moveBradeGrounds
}

automacro talkBradeonGroundsGetQuest {
	exclusive 1
	QuestInactive 7122
	InInventoryID 13040 = 0
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	NpcNear /Brade/
	call {
		do talk $.NpcNearLastBinId
		do conf route_randomWalk 1
		do conf itemsTakeAuto 2
		$potName = GetNamenyNameID(569)
		do conf useSelf_item_0 $potName
		do conf useSelf_item_0_disabled 0
		do conf useSelf_item_0_hp < 60%
		do conf getAuto_0 $potName
		do conf getAuto_0_minAmount 20
		do conf getAuto_0_maxAmount 200
		do conf getAuto_0_passive 0
		do mconf 1063 0 0 0
		do mconf 1002 1 0 0
		do mconf 1113 0 0 0
		call voltarAtacar
	}
}

automacro usarPocaoDeApreniz {
	ConfigKeyNot useSelf_item_0 Poção de Aprendiz
	InInventory "Poção de Aprendiz" > 0
	exclusive 1
	call {
		log parece que tenho poção de aprendiz, mas ainda não foi configurado pra ser usado
		log mudando isso
		$potName = GetNamenyNameID(569)
		do conf useSelf_item_0 $potName
		do conf useSelf_item_0_disabled 0
		do conf useSelf_item_0_hp < 60%
		do conf getAuto_0 $potName
		do conf getAuto_0_minAmount 20
		do conf getAuto_0_maxAmount 200
		do conf getAuto_0_passive 0
		log pronto
	}
}

automacro moveBradeCompleteQuest {
	exclusive 1
	QuestHuntCompleted 7122 1002
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	NpcNotNear /Brade/
	priority 1
	call moveBradeGrounds
}

macro moveBradeGrounds {
	do mconf 1063 0 0 0
	do mconf 1002 0 0 0
	do mconf 1113 0 0 0
	call pararDeAtacar
	do move 99 31
}

automacro talkBradeonGroundsCompleteQuest {
	exclusive 1
	QuestHuntCompleted 7122 1002
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	NpcNear /Brade/
	call {
		do talk $.NpcNearLastBinId
		do conf -f quest_atual lunaticos
	}
}

automacro equipStuffForBradeGrounds {
	exclusive 1
	QuestInactive 7122
	InInventoryID 13040 = 1
	IsNotEquippedID rightHand 13040
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		$check = GetIndexAndEquipped("rightHand", 13040)
		if ($check != -1) do eq $check
	}
}

automacro LastBradeBug {
	NpcMsg /Você ainda está no processo de treinamento/
	exclusive 1
	priority 0
	call {
		log Acabou de ocorrer um bug na sequencia do campo de treinamento
		log Resolvendo isso
		call moveBradeGrounds
		do talk &npc(/Brade/)
		do conf -f quest_atual lunaticos
	}
}

automacro questLunaticoBugged {
	QuestHuntCompleted 7122 1002
	ConfigKey quest_atual lunaticos
	exclusive 1
	call {
		log Acabou de ocorrer um bug na sequencia do campo de treinamento
		log Resolvendo isso
		call moveBradeGrounds
		do talk &npc(/Brade/)		
	}
}

# Quest de lunaticos , pickys e salgueiros by Nipodemos
# Inspirada na macro de 1 a 99 do CALF
automacro irAteQuestLunatico {
	ConfigKey quest_atual lunaticos
	exclusive 1
	NpcNotNear /Mago|Mage/
	QuestInactive 7124
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call moverPertoDosNpc
}

automacro começarQuestLunaticos {
	ConfigKey quest_atual lunaticos
	exclusive 1
	QuestInactive 7124
	NpcNear /Mago|Mage/
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	run-once 1
	call {
		do talk $.NpcNearLastBinId
		do talk resp 2
	}
}

automacro caçandoLunaticos {
	QuestActive 7124
	exclusive 1
	InMap new_1-3, new_2-3, new_3-3
	run-once 1
	call {
		[
		do mconf 1063 1 0 0 #lunatico
		do mconf 1002 0 0 0 #poring
		do mconf 1113 0 0 0 #drops
		]
		call voltarAtacar
		log ======== Indo matar lunaticos
	}
}

automacro lockMap setado quando nao deveria {
	ConfigKeyNot lockMap none
	exclusive 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		do conf lockMap none
		do conf attackAuto_inLockOnly 0
	}
	
}

macro moverPertoDosNpc {
	log indo pra perto dos Npcs
	call pararDeAtacar
	do move &rand(103,105) &rand(36,38)
	pause 1
}

automacro mateiLunaticosIndoAteoNpc {
	QuestHuntCompleted 7124 1063
	exclusive 1
	NpcNotNear /Mago|Mage/
	InMap new_1-3, new_2-3, new_3-3
	call moverPertoDosNpc
}

automacro mateiLunaticos {
	ConfigKey quest_atual lunaticos
	exclusive 1
	QuestHuntCompleted 7124 1063
	NpcNear /Mago|Mage/
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		do mconf 1063 0 0 0 #lunatico
		do talk $.NpcNearLastBinId
		do talk resp 2
		do conf -f quest_atual pickys
	}
}

automacro ComeçarQuestPickyMoverPerto {
	ConfigKey quest_atual pickys
	NpcNotNear /Espadachim|Swordman/
	QuestInactive 7123
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call moverPertoDosNpc
}

automacro começarQuestPicky {
	ConfigKey quest_atual pickys
	NpcNear /Espadachim|Swordman/
	QuestInactive 7123
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		log Indo pegar quest para  Picks
		pause 2
		do talk $.NpcNearLastBinId
		do talk resp 2
		do conf -f quest_atual salgueiros
	}
}

automacro começarQuestSalgueiroMoverPerto {
	NpcNotNear /Thief|Gatuno/
	ConfigKey quest_atual salgueiros
	QuestActive 7123
	QuestInactive 7127
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call moverPertoDosNpc
}

automacro começarQuestSalgueiro {
	ConfigKey quest_atual salgueiros
	QuestActive 7123
	QuestInactive 7127
	NpcNear /Thief|Gatuno/
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		log Indo pegar quest para  Salgueiro
		do talk $.NpcNearLastBinId
		do talk resp 2
		do conf -f quest_atual none
	}
}

automacro changeToHeavierTraining {
	QuestActive 7123
	QuestActive 7127
	exclusive 1
	NpcNear /Trainer/
	InMap new_1-3, new_2-3, new_3-3
	call {
		do move &rand(103,105) &rand(36,38)
		pause 3
		log mudando pra mapa onde tem pickys e salgueiros
		do talk $.NpcNearLastBinId #Treinador
		do talk resp 0
		do talk resp /5/
	}
}

automacro treinoMaisPesadoMoverPerto {
	QuestActive 7123
	QuestActive 7127
	exclusive 1
	NpcNotNear /Treinador/
	InMap new_1-3, new_2-3, new_3-3
	call moverPertoDosNpc
}

automacro treinoMaisPesado {
	QuestActive 7123
	QuestActive 7127
	exclusive 1
	NpcNear /Treinador/
	InMap new_1-3, new_2-3, new_3-3
	call {
		log mudando pra mapa onde tem pickys e salgueiros
		do talk $.NpcNearLastBinId #Treinador
		do talk resp 1
	}
}

automacro caçandoPickys {
	QuestHuntOngoing 7123 1049
	InMap new_4-3, new_5-3
	timeout 60
	exclusive 1
	call sePreparandoPraMatar 
}

automacro caçandoSalgueiro {
	QuestHuntOngoing 7127 1010
	InMap new_4-3, new_5-3
	timeout 60
	exclusive 1
	call sePreparandoPraMatar 
}

macro sePreparandoPraMatar {
	call voltarAtacar
	do mconf 1113 0 0 0 #drops
	do mconf 1004 0 0 0 #zangao
	do mconf 1007 0 0 0 #fabre
	do mconf $.QuestHuntOngoingLastMobID 1 0 0 
	log ======== Indo matar pickys e salgueiros
}

automacro mateiPickysIndoAteNpc {
	QuestHuntCompleted 7123 1049
	exclusive 1
	InMap new_4-3, new_5-3
	NpcNotNear /Espadachi|Swordman/i
	call moverPertoDosNpc
}

automacro mateiPickys {
	QuestHuntCompleted 7123 1049
	exclusive 1
	InMap new_4-3, new_5-3
	NpcNear /Espadachi|Swordman/i
	call {
		do mconf 1004 0 0 0 #zangao
		do mconf 1007 0 0 0 #fabre
		do mconf 1049 0 0 0 #Picky
		do talk $.NpcNearLastBinId
		do talk resp 2
		call voltarAtacar
	}
}

automacro mateiSalgueirosIndoAteNpc {
	QuestHuntCompleted 7127 1010
	exclusive 1
	InMap new_4-3, new_5-3
	NpcNotNear /Gatuno|Thief/i
	call moverPertoDosNpc
}

automacro mateiSalgueiros {
	QuestHuntCompleted 7127 1010
	exclusive 1
	NpcNear /Gatuno|Thief/i
	InMap new_4-3, new_5-3
	call {
		do mconf 1004 0 0 0 #zangao
		do mconf 1007 0 0 0 #fabre
		do mconf 1049 0 0 0 #Picky
		do mconf 1010 0 0 0 #Salgueiro
		do talk $.NpcNearLastBinId
		do talk resp 2
		log ======== Completei todas as Quests
		do conf quest_atual todas_completadas
	}
}

automacro completeiTodasAsQuests {
	JobLevel = 10
	BaseLevel < 12
	ConfigKeyNot quest_atual todas_completadas
	QuestInactive 7127
	QuestInactive 7123
	QuestInactive 7124
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		do conf quest_atual todas_completadas
	}
}

automacro continuarUpandoAte12 {
	ConfigKey quest_atual todas_completadas
	QuestInactive 7127
	QuestInactive 7123
	QuestInactive 7124
	BaseLevel < 12
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		[
		do mconf 1004 1 0 0 #zangao
		do mconf 1007 1 0 0 #fabre
		do mconf 1049 1 0 0 #Picky
		do mconf 1010 1 0 0 #Salgueiro
		do mconf 1063 1 0 0 #lunatico
		do mconf 1002 1 0 0 #poring
		do mconf 1113 1 0 0 #drops
		call voltarAtacar
		]
		log Vou continuar upando até chegar nivel 12
	}
}
automacro continuar upando ate 12 bugged {
	ConfigKey quest_atual todas_completadas
	QuestActive 7127, 7123, 7124
	BaseLevel < 12
	exclusive 1
	run-once 1
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	call {
		log Deveria upar ate o lvl 12 agora, mas nao completamos uma das quests
		log O número da quest é $.QuestActiveLastID
		log Resolvendo isso
		log Se vc ver essa mensagem mais de uma vez provavelmente a macro está bugada
		switch ( $.QuestActiveLastID ) {
			case (= 7124) {
				do conf quest_atual lunaticos
			}
			case (= 7123) {
				do conf quest_atual pickys
			}
			else {
				do conf quest_atual salgueiros
			}
		}
	}
}


automacro seMorrerEnquantoUpaAteLv12 {
	ConfigKey quest_atual todas_completadas
	CurrentHP >= 50%
	IsInCoordinate 95 21
	InMap new_1-3, new_2-3, new_3-3
	exclusive 1
	call mudarDeMapa
}

automacro se morrer enquanto mata pickys_e_salgueiros {
	QuestActive 7123, 7127
	CurrentHP >= 50%
	IsInCoordinate 95 21
	InMap new_1-3, new_2-3, new_3-3
	exclusive 1
	call mudarDeMapa
}

macro mudarDeMapa {
	do mconf 1063 0 0 0 #lunatico
	call pararDeAtacar
	do move &rand(103,105) &rand(36,38)
	log ======== mudando pra mapa onde tem pickys e salgueiros
	do talk &npc(/Treinador/) #Treinador
	do talk resp 1
	call voltarAtacar
}

automacro ifDieWhileKillPickyAndWillow {
	QuestActive 7123, 7127
	CurrentHP >= 50%
	IsInCoordinate 95 21
	InMap new_1-3, new_2-3, new_3-3
	exclusive 1
	call changeMap
}

automacro ifDieWhileLevelingto12 {
	ConfigKey quest_atual todas_completadas
	CurrentHP >= 50%
	IsInCoordinate 95 21
	InMap new_1-3, new_2-3, new_3-3
	exclusive 1
	call changeMap
}

macro changeMap {
	call pararDeAtacar
	do move &rand(103,105) &rand(36,38)
	log ======== mudando pra mapa onde tem pickys e salgueiros
	do talk &npc(/Trainer/) #Treinador
	do talk resp 0
	do talk resp /5/
	call voltarAtacar
}

automacro endleveling {
	exclusive 1
	JobLevel 10
	BaseLevel > 11
	ConfigKey quest_atual todas_completadas
	ConfigKey classe espadachim, classe gatuno, classe mercador, classe noviço, classe mago, classe arqueiro
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	priority 1
	call {
	    call pararDeAtacar
		do move &rand(103,105) &rand(36,38)
		switch (&config(classe)) {
			case (= espadachim) {
				do talk &npc(/Espadachim/)
			}
			case (= gatuno) {
				do talk &npc(/Gatuno/)
			}
			case (= mercador) {
				do talk &npc(/Mercador/)
			}
			case (= mago) {
				do talk &npc(/Mago/)
			}
			case (= noviço) {
				do talk &npc(/Noviço/)
			}
			case (= arqueiro) {
				do talk &npc(/Arqueiro/)
			}
		}
		do talk resp 1
		do talk resp 1
		log ======== Agora vou virar &config(classe)!!!
		do conf quest_atual none
	}
}

automacro endleveling2 {
	exclusive 1
	JobLevel 10
	ConfigKey quest_atual todas_completadas
	ConfigKey classe Espadachim, classe Gatuno, classe Mercador, classe Noviço, classe Mago, classe Arqueiro
	InMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	priority 1
	call {
	    call pararDeAtacar
		do move &rand(103,105) &rand(36,38)
		switch (&config(classe)) {
			case (= Espadachim) {
				do talk &npc(/Espadachim/)
			}
			case (= Gatuno) {
				do talk &npc(/Gatuno/)
			}
			case (= Mercador) {
				do talk &npc(/Mercador/)
			}
			case (= Mago) {
				do talk &npc(/Mago/)
			}
			case (= Noviço) {
				do talk &npc(/Noviço/)
			}
			case (= Arqueiro) {
				do talk &npc(/Arqueiro/)
			}
		}
		do talk resp 1
		do talk resp 1
		log ======== Agora vou virar &config(classe)!!!
		do conf quest_atual none
	}
}

automacro EndLeveling Bugged {
	JobLevel 10
	ConfigKey quest_eden todas_completadas
	ConfigKey classe none
	timeout 20
	exclusive 1
	call {
		log TEM UM PEQUENO ERRO
		log VOCE ESQUECEU DE COLOCAR NO CONFIG.TXT QUAL CLASSE QUER SER
		log PRA FAZER ISSO VC DIGITA AQUI NO CONSOLE
		log conf classe SEGUIDO DA CLASSE QUE QUER
		log VALORES POSSIVEIS: espadachim, mercador, mago, arqueiro, noviço, gatuno
		log EXEMPLO: conf classe espadachim
		log OUTRO EXEMPLO: conf classe mercador
		log ATE ISSO SER RESOLVIDO A MACRO NAO VAI PROSSEGUIR
	}
}

automacro seClasseNaoExistir {
	ConfigKeyNotExist classe
	exclusive 1
	priority -3
	call {
		log A config de classe ainda nao existe no config.txt
		log Vamos mudar isso
		add_key ("classe", "none")
		log Prontinho
	}
}

automacro virandoClasse1Bugged {
	JobLevel = 10
	InMapRegex /^(?!new_\d-\d)$/
	SkillLevel NV_BASIC != 9
	run-once 1
	exclusive 1
	call {
		$pontos = $.SkillLevelLastLevel
		log As Habilidades básicas nao foram distribuídas!!!
		log A skill ta lvl $.SkillLevelLastLevel
		log Mudando isso
		while ($pontos < 9 ) as loop
		do skills add NV_BASIC
		$pontos++
		end loop
	}
}

automacro virandoEspadachim {
	InMap izlude_in
	JobID 0 #Aprendiz
	JobLevel 10
	SkillLevel NV_BASIC = 10
	priority 1
	exclusive 1
	call {
		do talk &npc(/Espadachim/)
		do talk resp 1
		do talk resp 0
		pause 3
		call deixeiDeSerAprendiz
		$check = GetIndexAndEquipped("rightHand",13415) # alfange de iniciante
		if ($check != -1) do eq $check
		do iconf 13415 0 0 0
	}
}

automacro virandoNoviço {
	InMap prt_church
	JobLevel 10
	JobID 0
	SkillLevel NV_BASIC = 10
	priority 1
	exclusive 1
	call {
		do move prt_church &rand(182,184) &rand(36,38)
		do talk &npc(/Cleric|Padre/g)
		do talk resp 0
		pause 3
		call deixeiDeSerAprendiz
		$check = GetIndexAndEquipped("rightHand",1545) # Maça de iniciante
		if ($check != -1) do eq $check
		do iconf 1545 0 0 0
	}
}

automacro virandoMercador {
	InMap alberta_in
	JobLevel 10
	JobID 0
	SkillLevel NV_BASIC = 10
	exclusive 1
	priority 1
	call {
		do talk &npc(/Mercador/)
		do talk resp 0
		pause 3
		call deixeiDeSerAprendiz
		$check = GetIndexAndEquipped("rightHand",1381) # Machado de batalha de iniciante
		if ($check != -1) do eq $check
		do iconf 1381 0 0 0
	}
}

automacro virandoGatuno {
	JobID 0
	JobLevel 10
	InMap moc_prydb1
	priority 1
	SkillLevel NV_BASIC = 10
	exclusive 1
	call {
		do move &rand(44,46) &rand(127,129)
		do talknpc 42 133 c r1 c r1 c c c c  #Mestre Gatuno
		pause 3
		call deixeiDeSerAprendiz
		$check = GetIndexAndEquipped("rightHand",13041) # main gauche de iniciante
		if ($check != -1) do eq $check
		do iconf 13041 0 0 0
	}
}

automacro virandoArqueiro {
	JobID 0
	JobLevel 10
	InMap payon_in02
	exclusive 1
	priority 1
	call {
		do talk &npc(/Arqueiro/)
		do talk resp 0
		pause 3
		call deixeiDeSerAprendiz
		#1742
		$check = GetIndexAndEquipped("rightHand",1742) # arco composto de iniciante
		if ($check != -1) do eq $check
		do iconf 1742 0 0 0
		
	}
}

automacro virandoMago {
	JobID 0
	JobLevel 10
	InMap geffen_in
	exclusive 1
	priority 1
	call {
		do move &rand(162,165) &rand(123, 126)
		do talk &npc(/Instrutora/)
		do talk resp 0
		do talk resp 0
		pause 3
		call deixeiDeSerAprendiz
		$check = GetIndexAndEquipped("rightHand",1639) # cajado de iniciante
		if ($check != -1) do eq $check
		do iconf 1639 0 0 0
	}
}

macro deixeiDeSerAprendiz {
	do pconf 1243 0 #1243#Adaga_do_Aprendiz#
	do pconf 2112 0 #2112#Vembrassa_do_Aprendiz#
	do pconf 2414 0 #2414#Sandálias_do_Aprendiz#
	do pconf 2510 0 #2510#Capuz_do_Aprendiz#
	do pconf 2532 0 #2352#Traje_do_Aprendiz#
	do pconf 5055 0 #5055#Chapéu_de_Ovo_do_Aprendiz#
	do iconf 2393 0 0 0 #Tunica de Iniciante
	$check = GetIndexAndEquipped("rightHand", 1243) #Adaga_do_Aprendiz#
	if ($check != -1) do drop $check
	$check = GetIndexAndEquipped("leftHand", 2112)#Vembrassa_do_Aprendiz#
	if ($check != -1) do drop $check
	$check = GetIndexAndEquipped("shoes", 2414) #Sandálias_do_Aprendiz#
	if ($check != -1) do drop $check
	$check = GetIndexAndEquipped("robe", 2510) #Capuz_do_Aprendiz#
	if ($check != -1) do drop $check
	$check = GetIndexAndEquipped("armor", 2352) #Chapéu_de_Ovo_do_Aprendiz#
	if ($check != -1) do drop $check
	$check = GetIndexAndEquipped("topHead", 5055) #Chapéu_de_Ovo_do_Aprendiz#
	if ($check != -1) do drop $check
	$check = GetIndexAndEquipped("armor", 2393) #Tunica de Iniciante
	if ($check != -1) do eq $check
}

automacro upAteLv12Aprendiz {
	ConfigKeyNot lockMap pay_fild01
	ConfigKeyNot In_saveMap_sequence true
	JobLevel != 10
	JobID 0
	exclusive 1
	timeout 30
	InMapRegex /^(?!new_\d-\d|moc_prydb1)$/
	call {
		log pelo visto, ainda to no lvl de Classe $JobLevelLast ainda... 
		log o que aconteceu?
		log a que ponto chegamos?
		log nuss
		log Tentando resolver isso...
		call configurarMapa "payon" "pay_fild01"
	}
}


automacro upAteLv12 {
	ConfigKeyNot lockMap pay_fild01
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 1..11
	NotInMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		call configurarMapa "payon" "pay_fild01"
	}
}

automacro upAteLv26 {
	ConfigKeyNot lockMap pay_fild08
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 12..25
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		call configurarMapa "payon" "pay_fild08"
	}
}


automacro upAteLv35 {
	ConfigKeyNot lockMap pay_fild07
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 26..35
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		call configurarMapa "payon" "pay_fild07"
	}
}

automacro upAteLv44 {
	BaseLevel 36..43
	ConfigKeyNot lockMap pay_fild09
	ConfigKeyNot In_saveMap_sequence true
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		call configurarMapa "payon" "pay_fild09"
	}
}

automacro upAteLv54 {
	BaseLevel 44..53
	ConfigKeyNot lockMap gef_fild10
	ConfigKeyNot In_saveMap_sequence true
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		call configurarMapa "prontera" "gef_fild10"
	}
}

automacro upAteLv60 {
	ConfigKeyNot lockMap iz_dun02
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot virarArruaceiro true
	JobID 6, 17
	BaseLevel 54..59
	exclusive 1
	priority 5
	timeout 30
	call {
		call configurarMapa "izlude" "iz_dun02"
	}
}

automacro upAteLv70 {
	BaseLevel 60..70
	JobID 17
	ConfigKeyNot lockMap moc_fild17
	ConfigKeyNot In_saveMap_sequence true
	priority 5
	timeout 30
	call {
		call configurarMapa "morocc" "moc_fild17"
	}
}

automacro upAteLv80 {
	BaseLevel 71..79
	ConfigKeyNot lockMap mjolnir_11
	ConfigKeyNot In_saveMap_sequence true
	JobID 17
	exclusive 1
	priority 5
	timeout 30
	call {
		call configurarMapa "prontera" "mjolnir_11"
	}
}

automacro upAteLv92 {
	BaseLevel 80..91
	ConfigKeyNot lockMap gef_fild06
	ConfigKeyNot In_saveMap_sequence true
	JobID 17
	exclusive 1
	priority 5
	timeout 30
	call {
		call configurarMapa "geffen" "gef_fild06"
	}
}
automacro IndoPraRachelPraUpAteLv99 {
	ConfigKeyNot lockMap ra_fild12
	ConfigKeyNot aeroplano1 rachel
	BaseLevel 92..99
	NotInMap rachel
	NotInMap ra_fild12
	JobID 17
	exclusive 1
	priority 5
	timeout 30
	call {
		call junopra "rachel"
	}
}

automacro salvandoEmRachelPraUpAteLv99 {
	ConfigKeyNot saveMap rachel
	InMap rachel
	ConfigKeyNot aeroplano1 rachel
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 92..99
	JobID 17
	exclusive 1
	run-once 1
	call {
		call SetSaveIn "rachel"
	}
}

automacro upAteLv99 {
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot lockMap ra_fild12
	BaseLevel 92..99
	exclusive 1
	ConfigKey saveMap rachel
	call {
		call voltarAtacar
		do conf lockMap ra_fild12
	}
}

automacro estouLv99 {
	BaseLevel = 99
	exclusive 1
	timeout 15
	JobID 17 #arruaceiro
	call {
		log CHEGUEI NO LVL 99 FINALMENTE !!!!!!!
		log CARA ISSO LEVOU TEMPO PARA CAR**HO
	}
}

macro configurarMapa {
	if ( &config(saveMap) = $.param1 ) {
		do conf lockMap $.param2
		call voltarAtacar
	} else {
		call SetSaveIn $.param1
	}
}

automacro nivelDeClasse50 {
	JobLevel = 50
	exclusive 1
	timeout 20
	NotInMap izlude
	call {
		lock upComeço
		log SEU CHAR JÁ ESTÁ NO NIVEL 50 DE CLASSE
		log FAÇA A QUEST PARA VIRAR CLASSE 2 MANUALMENTE POR FAVOR
		log DEPOIS ABRA O BOT NOVAMENTE
		log ASSIM QUE ELE SE TORNAR UM CLASSE 2, A MACRO VOLTA A UPAR NORMALMENTE
		do conf lockMap none
		do conf attackAuto 1
		do conf route_randomWalk 0
		do move izlude
	}
}

automacro nivelDeClasse50EmIzlude {
	InMap izlude
	exclusive 1
	timeout 20
	call {
		log SEU CHAR JÁ ESTÁ NO NIVEL 50 DE CLASSE
		log FAÇA A QUEST PARA VIRAR CLASSE 2 MANUALMENTE POR FAVOR
		log DEPOIS ABRA O BOT NOVAMENTE
		log ASSIM QUE ELE SE TORNAR UM CLASSE 2, A MACRO VOLTA A UPAR NORMALMENTE
	}
}


macro pararDeAtacar {
	[
	do conf route_randomWalk 0
	do conf attackAuto 0
	]
}
macro voltarAtacar {
	[
	do conf route_randomWalk 1
	do conf attackAuto 2
	]
}
automacro ConfigWasWrong {
    exclusive 1
    overrideAI 1
    priority -5
    ConfigKeyNot autoTalkCont 0, attackAuto_inLockOnly 0
    call {
		log Tem uma config que está errada
		log A config é $.ConfigKeyNotLastKey
		log O valor que quero é $.ConfigKeyNotLastWantedValue
		log Mas o valor atual é $.ConfigKeyNotLastKeyValue
		log O index é $.ConfigKeyNotLastMemberIndex
		log Mudando valor da config $.ConfigKeyNotLastKey de $.ConfigKeyNotLastKeyValue para $.ConfigKeyNotLastWantedValue
		do conf $.ConfigKeyNotLastKey $.ConfigKeyNotLastWantedValue
	}
}


macro SetSaveIn {
	do conf -f saveMap_save_sequence c r0
	do conf -f saveMap_storage_sequence c r1
	switch ( $.param1 ) {
		case (=~ /einbroch/i ) {
			do conf -f saveMap_wanted einbroch
			do conf -f saveMap_kafra_position 242 205
			do conf -f saveMap_sellNpc_position einbroch 224 207
		}
		case (=~ /aldebaran/i ) {
			do conf -f saveMap_wanted aldebaran
			do conf -f saveMap_kafra_position 143 119
			do conf -f saveMap_sellNpc_position aldebaran 165 107
		}
		case (=~ /juno/i ) {
		    do conf -f saveMap_wanted yuno
			do conf -f saveMap_kafra_position 152 187
			do conf -f saveMap_sellNpc_position yuno_in01 25 34
		}
		case (=~ /rachel/i ) {
			do conf -f saveMap_wanted rachel
			do conf -f saveMap_kafra_position 109 138
			do conf -f saveMap_sellNpc_position rachel 65 80
		}
		case (=~ /comodo/i ) {
		    do conf -f saveMap_wanted comodo
			do conf -f saveMap_kafra_position 195 150
			do conf -f saveMap_sellNpc_position cmd_in01 79 182
		}
		case (=~ /prontera/i ) {
		    do conf -f saveMap_wanted prontera
			do conf -f saveMap_kafra_position 151 29
			do conf -f saveMap_sellNpc_position prontera 113 42
		}
		case (=~ /payon/i ) {
			do conf -f saveMap_wanted payon
			do conf -f saveMap_kafra_position 181 104
			do conf -f saveMap_sellNpc_position payon_in01 5 49
		}
		case (=~ /geffen/i ) {
			do conf -f saveMap_wanted geffen
			do conf -f saveMap_kafra_position 120 62
			do conf -f saveMap_sellNpc_position geffen_in 74 144
		}
		case (=~ /morocc|morroc|morooc/i ) {
			do conf -f saveMap_wanted morocc
			do conf -f saveMap_kafra_position 156 97
			do conf -f saveMap_sellNpc_position morocc_in 141 67
		}
		case (=~ /izlude/i ) {
			do conf -f saveMap_wanted izlude
			do conf -f saveMap_kafra_position 134 88
			do conf -f saveMap_sellNpc_position 105 99
		}
		case ( =~ /veins/i ) {
			do conf -f saveMap_wanted veins
			do conf -f saveMap_kafra_position 207 128
			do conf -f saveMap_sellNpc_position ve_in 243 303
		}
		else {
			log nome da cidade inválido, tente novamente...
			log Talvez o nome foi escrito errado?
			stop
		}
	}
	log Iniciando seguência de salvamento em &config(saveMap_wanted)
	$saveMap = &config(saveMap_wanted)
	do conf -f In_saveMap_sequence true
	
}

#### Kafra Save
automacro moveLocKafraOutside {
	exclusive 1
	ConfigKey In_saveMap_sequence true
	ConfigKeyNot saveMap_wanted none
	ConfigKeyNot saveMap $saveMap
	NotInMap $saveMap
	call move_to_near_kafra
}

automacro moveLocKafraInside {
	exclusive 1
	ConfigKeyNot saveMap $saveMap
	ConfigKeyNot saveMap_wanted none
	NpcNotNear /(Kafra Employee|Funcionária Kafra)/
	InMap $saveMap
	call move_to_near_kafra
}

macro move_to_near_kafra {
	do move $saveMap &config(saveMap_kafra_position) &random("1","2","3","4","5","6")
}

automacro talkKafraEmployee {
	ConfigKey In_saveMap_sequence true
	ConfigKeyNot saveMap $saveMap
	ConfigKeyNot saveMap_wanted none
	InMap $saveMap
	NpcNear /(Kafra Employee|Funcionária Kafra)/
	delay 2
	timeout 20
	call {
		log talking to kafra at '&arg("$.NpcNearLastPos", 1) &arg("$.NpcNearLastPos", 2)' using sequence '&config(saveMap_save_sequence)'
		do talknpc &arg("$.NpcNearLastPos", 1) &arg("$.NpcNearLastPos", 2) &config(saveMap_save_sequence)
	}
}

automacro SavedAtKafra {
	exclusive 1
	priority -5
	NpcMsgName /O seu Ponto de Retorno foi salvo/ /(Kafra Employee|Funcionária Kafra)/
	ConfigKeyNot saveMap $saveMap
	ConfigKey In_saveMap_sequence true
	InMap $saveMap
	call {
		do conf saveMap $saveMap
		do conf storageAuto_npc $saveMap &config(saveMap_kafra_position)
		do conf storageAuto 1
		do conf sellAuto 1
		do conf sellAuto_npc &config(saveMap_sellNpc_position)
		do conf storageAuto_npc_type 3
		do conf storageAuto_npc_steps &config(saveMap_storage_sequence)
		do conf In_saveMap_sequence false
		do conf saveMap_save_sequence none
		do conf saveMap_storage_sequence none
		do conf saveMap_wanted none
		do conf saveMap_kafra_position none
		do conf saveMap_sellNpc_position none
	}
}

#Digite no console do eventMacro junopra --cidade
#Pode ser tambem einbrochpra
#ou rachelpra
#se o nome estiver certo, ele vai mover-se pra cidade desejada
macro junopra {
	log Vamos ir de Juno para $.param1
	#Se a cidade for izlude ou rachel , ele pega o aeroplano respectivo
	if ( $.param1 =~ /izlude/i ) {
		do conf -f aeroplano1 izlude
		do conf -f aeroplano2 none
	} elsif ( $.param1 =~ /rachel/i ) {
		do conf -f aeroplano1 rachel
		do conf -f aeroplano2 none
	} elsif ( $.param1 =~ /hugel/i ) {
		do conf -f aeroplano1 none
		do conf -f aeroplano2 hugel
	} elsif ( $.param1 =~ /lighthalzen/i ) {
		do conf -f aeroplano1 none
		do conf -f aeroplano2 lighthalzen
	} elsif ( $.param1 =~ /einbroch/i ) {
		do conf -f aeroplano1 none
		do conf -f aeroplano2 eibroch
	} elsif ( $.param1 =~ /[jy]uno/) {
		log VOCÊ QUER IR DE JUNO PRA JUNO?
	log PORQUE VOCÊ FARIA ISSO?
	} else {
		[
		log Você digitou um nome inválido, por favor tente novamente
		log Os valores podem ser:
		log izlude , rachel , juno ou yuno 
		log hugel , lighthalzen ou einbroch
		log Escreva como quiser, desde que escreva certo
		]
		stop
	}
	$aeroplano1 = &config(aeroplano1)
	$aeroplano2 = &config(aeroplano2)
	if ($.map = airplane || $.map = airplane_01) {
	log ja estamos no aeroplano, só esperar agora
	} else {
		if ( $.map != yuno ) do move yuno
	}
	log $aeroplano1, $aeroplano2
}

macro einbrochpra {
	log Vamos ir de Eibroch para $.param1
	#se a cidade for juno hugel ou lighthalzen ele só pega 1 aeroplano
	# se for outra cidade, ele pega os dois aeroplanos
	if ( $.param1 =~ /izlude/i ) {
		do conf -f aeroplano1 izlude
		do conf -f aeroplano2 juno
	} elsif ($.param1 =~ /rachel/i ) {
		do conf -f aeroplano1 rachel
		do conf -f aeroplano2 juno
	} elsif ( $.param1 =~ /hugel/i ) {
		do conf -f aeroplano1 none
		do conf -f aeroplano2 hugel
	} elsif ( $.param1 =~ /lighthalzen/i ) {
		do conf -f aeroplano1 none
		do conf -f aeroplano2 lighthalzen
	} elsif ($.param1 =~ /einbroch/i ) {
		log VOCÊ QUER IR DE EIBROCH PRA EINBROCH?
	log PORQUE VOCÊ FARIA ISSO?
	} elsif ( $.param1 =~ /[jy]uno/) {
		do conf -f aeroplano1 none
		do conf -f aeroplano2 juno
	} else {
		[
		log Você digitou um nome inválido, por favor tente novamente
		log Os valores podem ser izlude , rachel , juno ou yuno , 
		log hugel , lighthalzen ou einbroch
		log pode ser com letra maiuscula ou sem
		]
		stop
	}
	$aeroplano1 = &config(aeroplano1)
	$aeroplano2 = &config(aeroplano2)
	if ($.map = airplane || $.map = airplane_01) {
	log ja estamos no aeroplano, só esperar agora
	} else {
		do move airplane
	}
	log $aeroplano1, $aeroplano2
}

macro rachelpra {
	log Vamos ir de Rachel para $.param1
	#se a cidade for izlude ou juno ele só pega 1 aeroplano
	# se for outra cidade, ele pega os dois aeroplanos
	if ( $.param1 =~ /izlude/i ) {
		do conf -f aeroplano1 izlude
		do conf -f aeroplano2 none
	} elsif ($.param1 =~ /rachel/i ) {
		log VOCÊ QUER IR DE RACHEL PRA RACHEL?
	log PORQUE VOCÊ FARIA ISSO?
	} elsif ( $.param1 =~ /hugel/i ) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 hugel
	} elsif ( $.param1 =~ /lighthalzen/i ) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 lighthalzen
	} elsif ($.param1 =~ /einbroch/i ) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 eibroch
	} elsif ( $.param1 =~ /[jy]uno/) {
		do conf -f aeroplano1 juno
		do conf -f aeroplano2 none
	} else {
		[
		log Você digitou um nome inválido, por favor tente novamente
		log Os valores podem ser izlude , rachel , juno ou yuno , 
		log hugel , lighthalzen ou einbroch
		log pode ser com letra maiuscula ou sem
		]
		stop
	}
	$aeroplano1 = &config(aeroplano1)
	$aeroplano2 = &config(aeroplano2)
	if ( $.map = airplane || $.map = airplane_01) {
	log ja estamos no aeroplano, só esperar agora
	} else {
		do move ra_fild12 294 208
		do talk resp 0
		log $aeroplano1, $aeroplano2
	}
}

automacro dentroDoAeroplanoInternacional {
	InMap airplane_01
	LocalMsg /Bem-vindos a $aeroplano1|Welcome to $aeroplano1/i
	exclusive 1
	call sairDoAeroplano
}

automacro dentroDoAeroplanoDomestico {
	InMap airplane
	LocalMsg /Bem-vindos a $aeroplano2|Welcome to $aeroplano2/i
	exclusive 1
	call sairDoAeroplano
}

macro sairDoAeroplano {
	do move 243 74
}

#Se estivermos na cidade, e o local desejado for essa cidade, ele completa o objetivo
#se estivermos na cidade, mas nao for o objetivo, nada acontece, já que provavelmente
#ele só está se locomovendo pra fazer qualquer outra coisa aleatoria que nao tem nada
# a ver com esse macro
automacro chegueiEmEinbroch {
	InMap einbroch
	ConfigKey aeroplano2 einbroch
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmHugel { 
	InMap hugel
	ConfigKey aeroplano2 hugel 
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmIzlude {
	InMap izlude
	ConfigKey aeroplano1 izlude
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmLighthalzen {
	InMap lighthalzen
	ConfigKey aeroplano2 lighthalzen
	run-once 1
	exclusive 1
	call objetivoCompleto
}

automacro chegueiEmRachel {
	InMap ra_fild12
	ConfigKey aeroplano1 rachel
	run-once 1
	exclusive 1
	call {
		do move rachel
		call objetivoCompleto
	}
}

macro objetivoCompleto {
	#Se estivermos na cidade, e o local desejado for essa cidade, ele completa o objetivo 
	log cheguei em $.InMapLast , objetivo completo
	do conf aeroplano2 none
	do conf aeroplano1 none
}

automacro chegueiEmJuno {
	InMap yuno
	ConfigKeyNot aeroplano1 none, aeroplano2 none
	exclusive 1
	call {
		# é aqui que ocorre a mágica, quando chega em juno
		# ele detecta se precisa pegar outro aeroplano ou não
		# se ele precisar ele vai
		# se não precisar ele vai pra proximo da kafra e completa o objetivo
		log destino para aeroplano1 é: &config(aeroplano1)
		log destino para aeroplano2 é: &config(aeroplano2)
		if (&config(aeroplano1) =~ /rachel|izlude/i  ) {
		do move airplane_01
		} elsif ( &config(aeroplano2) =~ /hugel|einbroch|lighthalzen/i ) {
		do move airplane
		} elsif ( &config(aeroplano1) =~  /[yj]uno/i || &config(aeroplano2) =~ /[yj]uno/i ) {
			do move &rand(150, 154) &rand(182, 186)
		call objetivoCompleto
		} else {
			log Falha em descobrir a rota correta, revise o código
		}
	}
}

sub GetIndexAndEquipped {
	my ($type, $id) = @_;
	my $Item = $char->inventory->getByNameID($id);
	my $EquipIndex = $Item->{invIndex};
	if (my $equipItem = $char->{equipment}{$1}) {
		if ($equipItem->{nameID} == $id) {
		return -1;
		} else {
			return $EquipIndex;
		}
		} else {
			return $EquipIndex;
		}
}

sub GetNamenyNameID {
	my $name = $items_lut{$_[0]};
	return $name;
}

sub nextMap {
	my $map = $_[0];
	if ($map =~ /^new_(\d)-(\d)$/) {
	return "new_".$1."-".($2+1);
	} else {
		return 0;
	}
}

sub add_key {
	my ($key, $value) = @_;
	configModify($key, $value);
}

#########################################################################
# KvM macro by Revok
# Openkore: http://openkore.com/
# Revok: http://revok.com.br
# Openkore Private Forums: http://forum.revok.com.br
#
# Adaptado para o eventMacro por Nipodemos
# Reescrito praticamente, para aproveitar
# o potencial máximo que o eventMacro proporciona
#
# This source code is licensed under the
# GNU General Public License, Version 3.
# See http://www.gnu.org/licenses/gpl.html
#############################################################################


########################################
#                                      #
#       PRIMEIRAS CONFIGURAÇÕES        #
#                                      #
########################################

automacro not on bat_room {
	NotInMap bat_room
	NotInMap bat_c01
	ConfigKeyNot exercito none
	exclusive 1
	call {
		do move bat_room
	}
}

automacro ConfigKeyNao {
	ConfigKeyNot lockMap none, autoTalkCont 1
	ConfigKey exercito Croix, exercito Guillaume
	exclusive 1
	call {
		do conf route_randomWalk 0
		do conf attackAuto 1
		do conf lockMap none
		do conf autoTalkCont 1
	}
}

automacro army_is_Unset {
	exclusive 1
	ConfigKey exercito none
	call {
		[
		log ===========================================
		log Exercito não está definido, resolvendo isso
		log ===========================================
		]
		$army = &random ("Croix", "Guillaume")
		do conf -f exercito $army
	}
}


########################################################
#                                                      #
#                    SALÃO PRINCIPAL                   #
#                                                      #
########################################################

# Bugou Lado (Trocando)
automacro BugouLado1 {
	NpcMsg /^Eu sei que você já está ao lado dos Guillaume/i
	timeout 10
	exclusive 1
	call {
		log bug bug e mais bug kkkk
		log resolvendo
		do conf -f exercito Guillaume
	}
}

# Bugou Lado (Trocando)
automacro BugouLado2 {
	NpcMsg /^Eu sei que você já está ao lado dos Croix/i
	exclusive 1
	call {
		log bug bug e mais bug kkkk
		log resolvendo
		do conf -f exercito Croix
	}
}

automacro moverPertoDoNpcCroix {
	NpcNotNear /KVM.Croix/
	ConfigKey exercito Croix
	ConfigKeyNot chatNear sim
	InChatRoom 0
	exclusive 1
	NotInMap bat_c01
	InMap bat_room
	call {
		[
		log ============================================
		log Exercito selecionado: &config(exercito)
		log movendo-se pra proximo do npc KVM 
		log ============================================
		]
		do move bat_room &rand (164,167) &rand (124, 129) # move para perto dos NPCs
	}
}

automacro acessoAminiSalaCroix {
	NpcNear /KVM.Croix/
	exclusive 1
	InChatRoom 0
	QuestInactive 6025
	NotInMap bat_c01
	InMap bat_room
	ConfigKey exercito Croix
	call {
		[
		log ==========================================
		log Acessando a salinha do exercito &config(exercito)
		log movendo-se pra proximo do npc KVM 
		log ==========================================
		]
		do talk $.NpcNearLastBinId # Oficial de KVM
		do talk resp 0
	}
}

automacro moverPertoDoNpcGuillaume {
	NpcNotNear /KVM.Guillaum/
	InChatRoom 0
	ConfigKey exercito Guillaume
	ConfigKeyNot chatNear sim
	exclusive 1
	NotInMap bat_c01
	InMap bat_room
	call {
		[
		log =============================================
		log Exercito selecionado: &config(exercito)
		log movendo-se pra proximo do npc KVM 
		log =============================================
		]
		do move bat_room &rand (164,167) &rand (175, 170) # move para perto dos NPCs
	}
}

automacro acessoAminiSalaGuillaume {
	NpcNear /KVM.Guillaum/
	exclusive 1
	InChatRoom 0
	NotInMap bat_c01
	InMap bat_room
	ConfigKey exercito Guillaume
	call {
		[
		log ==========================================
		log Acessando a salinha do exercito &config(exercito)
		log ==========================================
		]
		do talk $.NpcNearLastBinId # Oficial de KVM
		do talk resp 0
	}
}

#######################################
#              MINI ROOM              #
#######################################

automacro miniROOM_TentarEntrarNoChat {
	macro_delay 0.2
	timeout 0.3
	priority -5 #prioridade máxima
	ChatRoomNear /KVM/
	InChatRoom 0
	InMap bat_room 
	exclusive 1
	run-once 1
	call {
		log posição atual: $.map $.pos, || nº aleatório: &rand(1,3)
		if ( &config (chatNear) != sim) do conf -f chatNear sim
		do chat join 0
		release miniROOM_TentarEntrarNoChat
	}
}


automacro miniROOM_entrouNoChat {
	InChatRoom 1
	run-once 1
	call {
		if (&config(sitAuto_idle) = 1) do conf sitAuto_idle 0
		[
		log ============================================
		log ==========ENTREI NA SALA DE CHAT============
		log ============================================
		]
	}
}

automacro começoDaBatalha {
	LocalMsg /Em 1 minuto\, o KVM vai começar/i
	exclusive 1
	call {
		do conf PegarPontos none
		release miniROOM_entrouNoChat
		do conf chatNear none
		log ==================================
		log estou dentro do campo de batalha
		log ==================================
		log localização atual: $.pos
	}
}



############################################
#                                          #
#            FIM DA BATALHA                #
#                                          #
############################################
automacro FimDaBatalha_pegarPontos {
	LocalMsg /Sem falar com o Oficial\, você não ganhará os/i
	InMap bat_c01
	timeout 3
	exclusive 1
	call {
		[
		log ================================================
		log A batalha acabou, pegando meus pontos de direito
		log ================================================
		]
		do conf -f PegarPontos sim
	}
}

automacro tentarPegarPontosQueMereço {
	ConfigKey PegarPontos sim
	timeout 5
	InMap bat_c01
	NpcNear /KVM/
	call {
		do talk 12
		do talk 13
		do talk 14
	}
}

automacro derrota {
	exclusive 1
	timeout 10
	NpcMsg /pontos pela derrota/i
	call {
		do conf PegarPontos none
		$points = &eval(&config(pontosKVM) + 1)
		#log \$points: $points
		do conf -f pontosKVM $points
		$oldarmy = &config (exercito)
		if ( &config (exercito) == Croix ) {
			do conf exercito Guillaume
		} elsif ( &config (exercito) == Guillaume ) {
			do conf exercito Croix
		}
		[
		log ================================
		log perdi a batalha =/
		log melhor trocar de lado
		log antes eu estava no exercito $oldarmy
		log agora estou no exercito &config(exercito)
		log Pontuação total até agora: [ &config(pontosKVM) ] pontos.
		log ================================
		]
	}
}


automacro empate {
	exclusive 1
	timeout 10
	NpcMsg /pontos pelo empate/i
	call {
		do conf PegarPontos none
		$points = &eval(&config(pontosKVM) + 1)
		log $points
		do conf -f pontosKVM $points
		[
		log ===========================
		log a batalha deu empate!!
		log Pontuação total até agora: [ &config(pontosKVM) ] pontos.
		log ===========================
		]
	}
}


automacro vitoria {
	exclusive 1
	timeout 10
	NpcMsg /pontos pela vitória/i
	call {
		do conf PegarPontos none
		$points = &eval(&config(pontosKVM) + 5)
		do conf -f pontosKVM $points
		log Ganhamos no &config(exercito) !
		[
		log ================================
		log Ganhamos a batalha!!!!! (exercito: &config(exercito))
		log apesar que eu só fiquei parado né
		log mas ganhamos!
		log Pontuação total até agora: [ &config(pontosKVM) ] pontos.
		log ================================
		]
	}
}

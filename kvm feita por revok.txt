#########################################################################
# KvM macro by Revok
# Openkore: http://openkore.com/
# Revok: http://revok.com.br                                                                             
# Openkore Private Forums: http://forum.revok.com.br
#
# Adaptado para o eventMacro por Nipodemos
# Reescrito praticamente, para aproveitar 
# o potencial máximo que o eventMacro proporciona
#
# This source code is licensed under the                                                                        
# GNU General Public License, Version 3.                                                                        
# See http://www.gnu.org/licenses/gpl.html                                                                 
#############################################################################


########################################
#                                      #
#       PRIMEIRAS CONFIGURAÇÕES        #
#                                      #
########################################

automacro not on bat_room {
	NotInMap bat_room
	NotInMap bat_c01
	ConfigKeyNot exercito none
	exclusive 1
	call {
		do move bat_room
	}
}

automacro ConfigKeyNao {
	ConfigKeyNot lockMap none, autoTalkCont 1
	ConfigKey exercito Croix, exercito Guillaume
	exclusive 1
	call {
		do conf route_randomWalk 0
		do conf attackAuto 1
		do conf lockMap none
		do conf autoTalkCont 1
	}
}

automacro army_is_Unset {
	exclusive 1
	ConfigKey exercito none
	call {
		$army = &random ("Croix", "Guillaume")
		do conf -f exercito $army
	}
}


########################################################
#                                                      #
#                    SALÃO PRINCIPAL                   #
#                                                      #
########################################################

automacro moverPertoDoNpcCroix {
	NpcNotNear /KVM.Croix/
	ConfigKey exercito Croix
	ConfigKeyNot dentroDaMiniRoom sim
	InChatRoom 0
	exclusive 1
	NotInMap bat_c01
	InMap bat_room
	call {
		do move bat_room &rand (164,167) &rand (124, 129) # move para perto dos NPCs
	}
}

automacro acessoAminiSalaCroix {
	NpcNear /KVM.Croix/
	exclusive 1
	InChatRoom 0
	QuestInactive 6025
	NotInMap bat_c01
	InMap bat_room
	ConfigKey exercito Croix
	call {
		do talk $.NpcNearLastBinId # Oficial de KVM
		do talk resp 0
	}
}

automacro moverPertoDoNpcGuillaume {
	NpcNotNear /KVM.Guillaum/
	InChatRoom 0
	ConfigKey exercito Guillaume
	ConfigKeyNot dentroDaMiniRoom sim
	exclusive 1
	NotInMap bat_c01
	InMap bat_room
	call {
		do move bat_room &rand (164,167) &rand (175, 170) # move para perto dos NPCs
	}
}

automacro acessoAminiSalaGuillaume {
	NpcNear /KVM.Guillaum/
	exclusive 1
	InChatRoom 0
	NotInMap bat_c01
	InMap bat_room
	ConfigKey exercito Guillaume
	call {
		do talk $.NpcNearLastBinId # Oficial de KVM
		do talk resp 0
	}
}

#######################################
#              MINI ROOM              #
#######################################

automacro miniROOM_TentarEntrarNoChat {
	macro_delay 0.2
	timeout 0.5
	priority -5 #prioridade máxima
	ChatRoomNear /KVM/
	InChatRoom 0
	exclusive 1
	run-once 1
	call {
		if ( &config (dentroDaMiniRoom) != sim) do conf -f dentroDaMiniRoom sim
		do chat join 0
		release miniROOM_TentarEntrarNoChat
	}
}


automacro miniROOM_entrouNoChat {
	InChatRoom 1
	run-once 1
	call {
		[
		log ============================================
		log ==========ENTREI NA SALA DE CHAT============
		log ============================================
		]
		}
	}

automacro FimDaBatalha_pegarPontos {
	NpcNear /KVM/
	InMap bat_c01
	timeout 10
	call {
		do conf dentroDaMiniRoom none
		release miniROOM_entrouNoChat
		do talk $.NpcNearLastBinId
	}
}


automacro derrota {
	exclusive 1
	NpcMsg /Você ganhou pontos pela derrota\: 1./i
	call {
		$points = &eval(&config(pontosKVM) + 1)
		log \$points: $points
		do conf -f pontosKVM $points
		$oldarmy = &config (exercito)
		if ( &config (exercito) == Croix ) do conf exercito Guillaume
		if ( &config (exercito) == Guillaume ) do conf exercito Croix
		log Perdemos no $oldarmy .\nAgora somos $army .\nPontuação total até agora: [ &config(pontosKVM) ] .
	}
}


automacro empate {
	exclusive 1
	NpcMsg /Você ganhou pontos pelo empate\: 1./i
	call {
		$points = &eval(&config(pontosKVM) + 1)
		log $points
		do conf -f pontosKVM $points
		log Empatamos no $army \nPontuação total até agora: [ &config(pontosKVM) ] .
	}
}


automacro vitoria {
	exclusive 1
	NpcMsg /Você ganhou pontos pela vitória\: 5./i
	call {
		$points = &eval(&config(pontosKVM) + 5)
		log $points
		do conf -f pontosKVM $points
		log Ganhamos no $army !\nPontuação total até agora: [ &config(kvmPoints) ] .
	}
}																									